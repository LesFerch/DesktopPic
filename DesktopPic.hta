<!DOCTYPE html>
<!--
DesktopPic (Display picture or slideshow on desktop and/or stamp picture to wallpaper)
Les Ferch, lesferch@gmail.com, GitHub repository created 2022-03-20
DesktopPic.hta (Main program to display and stamp images)
WallP.exe (command line tool to set wallpaper per monitor)
-->
<html>
<head>
<title>DesktopPic</title>
<meta charset="UTF-8" http-equiv="X-UA-Compatible" content="IE=9">
<hta:application
  id=oHTA
  icon="C:\Program Files (x86)\Microsoft\Edge\Application\msedge.exe"
  maximizebutton=no
  minimizebutton=no
  showintaskbar=no
  contextmenu=no
  border=none
  caption=no
  scroll=no
>
<script language="VBScript">

'Begin Main

Window.ResizeTo 0,0
Const HideMe = 999999999
Window.MoveTo HideMe,HideMe

Const MyVerNum = "1.1.5"
Const WallPVerNum = "1.0.0.0"
Const ForReading = 1
Const ForWriting = 2
Const wiaFormatPNG = "{B96B3CAF-0728-11D3-9D7B-0000F81EF32E}"
Const wiaFormatBMP = "{B96B3CAB-0728-11D3-9D7B-0000F81EF32E}"

Set oWSH = CreateObject("Wscript.Shell")
Set oFSO = CreateObject("Scripting.FileSystemObject")
Set oImg = CreateObject("WIA.ImageFile")
Set oSettings = CreateObject("Scripting.Dictionary")
Set DigitsOnly = New RegExp: DigitsOnly.Pattern = "[^0-9]"
Set DigitsComma = New RegExp: DigitsComma.Pattern = "[^0-9,]"

Dim PicTimer,UpdatePos,w,h,iw,ih,x,y,CenterCoordinates,hTpp,wTpp,Msg,L(8),aImage,MyAppData,ArrPic
Z = VBCRLF
Q = Chr(34)

'Copy of HTA for interface, WallP.exe, and processed image, go in MyAppData 
'Note: INI file is stored with app for portability and to allow multiple instances

MyAppData  = oWSH.ExpandEnvironmentStrings("%LocalAppData%") & "\DesktopPic\"
If Not oFSO.FolderExists(MyAppData) Then oFSO.CreateFolder(MyAppData)
WallPExe = MyAppData & "WallP.exe"
PicListFile = MyAppData & "PicList.txt"
NTVer = oWSH.RegRead("HKLM\Software\Microsoft\Windows NT\CurrentVersion\CurrentVersion")

L(0)="Run on startup|Bring to front on start|Stretch smaller pictures|Pic delay in seconds|Max percent of screen area|Centered|Custom|Window position|Stamp|Restore|Continue|Help|Place at least one image file in the folder shown below|No image files found|This option can only be used on the primary monitor.|Press ⊞-P to temporarily change the primary monitor.|Wallpaper style must be set to Fill to use this option|Change wallpaper style to Fill?|Wallpaper file missing:|Background type must be set to Picture to use this option|Change background type to picture?|Display file names"
L(1)="Beim Start ausführen|Beim Start nach vorne bringen|Strecken Sie kleinere Bilder|Bildverzögerung in Sekunden|Maximaler Prozentsatz der Bildschirmfläche|Zentriert|Brauch|Fensterposition|Briefmarke|Wiederherstellen|Fortsetzen|Hilfe|Legen Sie mindestens eine Bilddatei in dem unten angezeigten Ordner ab|Keine Bilddateien gefunden|Diese Option kann nur auf dem primären Monitor verwendet werden|Drücken Sie ⊞-P, um den primären Monitor vorübergehend zu ändern.|Hintergrundstil muss auf Ausfüllen eingestellt sein, um diese Option zu verwenden|Hintergrundstil in Ausfüllen ändern?|Wallpaper-Datei fehlt:|Der Hintergrundtyp muss auf Bild eingestellt sein, um diese Option zu verwenden|Hintergrundtyp in Bild ändern?|Dateinamen anzeigen"
L(2)="Corre al empezar|Traer al frente al inicio|Estirar imágenes más pequeñas|Retardo de imagen en segundos|Porcentaje máximo del área de la pantalla|Centrado|Personalizado|Posición de la ventana|Sello|Restaurar|Continuar|Ayuda|Coloque al menos un archivo de imagen en la carpeta que se muestra a continuación|No se encontraron archivos de imagen|Esta opción solo se puede usar en el monitor principal|Presione ⊞-P para cambiar temporalmente el monitor principal.|El estilo de fondo de pantalla debe establecerse en Rellenar para usar esta opción|¿Cambiar el estilo de fondo de pantalla a Rellenar?|Falta el archivo del fondo de pantalla:|El tipo de fondo debe establecerse en Imagen para usar esta opción|¿Cambiar el tipo de fondo a imagen?|Mostrar nombres de archivos"
L(3)="Exécuter au démarrage|Amenez à l'avant au démarrage|Étirez des images plus petites|Délai d'image en secondes|Pourcentage maximum de la zone d'écran|Centré|Personnalisé|Position de la fenêtre|Timbre|Restaurer|Continuez|Aide|Placez au moins un fichier image dans le dossier indiqué ci-dessous|Aucun fichier image trouvé|Cette option ne peut être utilisée que sur le moniteur principal|Appuyez sur ⊞-P pour changer temporairement le moniteur principal.|Le style de papier peint doit être défini sur Remplir pour utiliser cette option|Changer le style de fond d'écran en Remplir ?|Fichier de fond d'écran manquant :|Le type d'arrière-plan doit être défini sur Image pour utiliser cette option|Changer le type d'arrière-plan en image ?|Afficher les noms de fichiers"
L(4)="起動時に実行する|スタート時に前面に出す|小さい画像を伸ばす|秒単位の画像の遅延|画面領域の最大パーセント|中央揃え|カスタム|ウィンドウの位置|切手|戻す|継続する|ヘルプ|以下に示すフォルダに少なくとも1つの画像ファイルを配置します|画像ファイルが見つかりません|このオプションは、プライマリモニターでのみ使用できます|⊞-Pを押して、プライマリモニターを一時的に変更します。|このオプションを使用するには、壁紙のスタイルを塗りつぶしに設定する必要があります|壁紙のスタイルを塗りつぶしに変更しますか？|壁紙ファイルがありません:|このオプションを使用するには、背景タイプを画像に設定する必要があります|背景の種類を画像に変更しますか？|ファイル名を表示する"
L(5)="在启动时运行|开始时放在前面|拉伸较小的图片|图片延迟以秒为单位|最大屏幕面积百分比|居中|风俗|窗口位置|邮票|恢复|继续|帮助|将至少一个图像文件放入如下所示的文件夹中|未找到图像文件|此选项只能在主显示器上使用|按 ⊞-P 临时更改主监视器。|墙纸样式必须设置为填充才能使用此选项|将壁纸样式更改为填充？|壁纸文件丢失：|背景类型必须设置为图片才能使用此选项|将背景类型更改为图片？|显示文件名"
L(6)="在啟動時運行|開始時放在前面|拉伸較小的圖片|圖片延遲以秒為單位|最大屏幕面積百分比|居中|風俗|窗口位置|郵票|恢復|繼續|幫助|在如下所示的文件夾中至少放置一個圖像文件|未找到圖像文件|此選項只能在主顯示器上使用|按 ⊞-P 臨時更改主監視器。|牆紙樣式必須設置為填充才能使用此選項|將壁紙樣式更改為填充？|壁紙文件丟失：|背景類型必須設置為圖片才能使用此選項|將背景類型更改為圖片？|顯示文件名"
L(7)="시작 시 실행|시작 시 맨 앞으로 가져오기|작은 사진 늘리기|초 단위의 사진 지연|화면 영역의 최대 백분율|중앙|관습|창 위치|우표|복원하다|계속하다|돕다|아래 표시된 폴더에 하나 이상의 이미지 파일을 넣으십시오.|이미지 파일을 찾을 수 없습니다|이 옵션은 기본 모니터에서만 사용할 수 있습니다.|기본 모니터를 일시적으로 변경하려면 ⊞-P를 누르세요.|이 옵션을 사용하려면 배경 화면 스타일을 채우기로 설정해야 합니다.|배경화면 스타일을 채우기로 변경하시겠습니까?|배경화면 파일 누락:|이 옵션을 사용하려면 배경 유형을 그림으로 설정해야 합니다.|배경 유형을 그림으로 변경하시겠습니까?|파일 이름 표시"
L(8)="Chạy khi khởi động|Đưa lên trước khi bắt đầu|Kéo dài hình ảnh nhỏ hơn|Pic trễ trong vài giây|Phần trăm diện tích màn hình tối đa|Căn giữa|Tập quán|Vị trí cửa sổ|Con tem|Khôi phục|Còn nữa|Giúp đỡ|Đặt ít nhất một tệp hình ảnh trong thư mục được hiển thị bên dưới|Không tìm thấy tệp hình ảnh nào|Tùy chọn này chỉ có thể được sử dụng trên màn hình chính.|Nhấn ⊞-P để tạm thời thay đổi điều khiển chính.|Kiểu hình nền phải được đặt thành Tô để sử dụng tùy chọn này|Thay đổi kiểu hình nền thành Điền?|Tệp hình nền bị thiếu:|Loại nền phải được đặt thành Ảnh để sử dụng tùy chọn này|Thay đổi loại nền thành hình ảnh?|Hiển thị tên tệp"

ShowFileName = False
RunOnStartup = False
BringToFront = True
Stretch = False
PicDelay = 5
MaxScrPct = 25
Coordinates = ""
Forward = True
DoingInput = False
BadImage = False
PicIndex = -1
LangIdx=0
On Error Resume Next
Language = oWSH.RegRead("HKCU\Control Panel\International\LocaleName")
Language = oWSH.RegRead("HKCU\Control Panel\Desktop\PreferredUILanguages")(0)
If Left(Language,2)="de" Then LangIdx=1
If Left(Language,2)="es" Then LangIdx=2
If Left(Language,2)="fr" Then LangIdx=3
If Left(Language,2)="ja" Then LangIdx=4
If Language="zh-CN" Then LangIdx=5
If Language="zh-TW" Then LangIdx=6
If Left(Language,2)="ko" Then LangIdx=7
If Left(Language,2)="vi" Then LangIdx=8
On Error Goto 0

OptionMode = oHTA.caption="yes"
BorderSize = 0: If OptionMode Then BorderSize = 8
CaptionHeight = 30

CmdLine = Replace(oHTA.commandLine,Q & "  " & Q,"|")
CmdLine = Replace(CmdLine,Q & " " & Q,"|")
CmdLine = Replace(CmdLine,Q,"")
aParam = Split(CmdLine,"|")

MyPath  = Mid(document.URL,8)
MyFolder = oFSO.GetParentFolderName(MyPath)
MyName = oFSO.GetFileName(MyPath)
OptionModeHTA = MyAppData & MyName
If oFSO.FileExists(MyFolder & "\README.md") Then DevMode = True 

If UBound(aParam)>0 Then
  If IsNumeric(aParam(1)) Then PicIndex = aParam(1) - 1
End If

If OptionMode Then
  PicPath = aParam(2) & "\"
  HTAName = aParam(3)
  MyTitle = Split(HTAName,".")(0)
  oWSH.CurrentDirectory = PicPath
Else
  MyTitle = Split(MyName,".")(0)
  oWSH.CurrentDirectory = MyFolder
  PicPath = oWSH.CurrentDirectory & "\"
End If

INIFile = PicPath & MyTitle & ".ini"

If Not(Instr(MyFolder,"C:\Program Files\")>0 Or Instr(MyFolder,"C:\Program Files (x86)\")>0) Then
  TestFile = ".\" & Replace(Replace(Replace(Now(),"/",""),":","")," ","") & ".txt"
  On Error Resume Next
  Set oFile = oFSO.OpenTextFile(TestFile,ForWriting,True,Ansi)
  oFile.Write ""
  oFile.Close
  On Error Goto 0
  If oFSO.FileExists(TestFile) Then
    oFSO.DeleteFile(TestFile)
  Else
    INIFile = MyAppData & MyTitle & ".ini"
  End If
End If

If NTVer=>"6.1" Then
  If oFSO.FileExists(WallPExe) Then
    Set oFile = oFSO.OpenTextFile(WallPExe,ForReading)
    FileContents = oFile.ReadAll
    oFile.Close
    If Instr(FileContents,WallPVerNum)=0 Then oFSO.DeleteFile(WallPExe)
  End If  

  If Not oFSO.FileExists(WallPExe) Then
    oWSH.Run "certutil -decode " & Q & MyPath & Q & " " & Q & WallPExe & Q,0,False
  End If
End If

PicExt = ".png"
PicFormat = wiaFormatPNG
If NTVer="6.1" Then
  PicFormat = wiaFormatBMP
  PicExt = ".bmp"
End If

Ini2Dict

If oSettings.Exists("[Options]LangIdx") Then LangIdx = oSettings.Item("[Options]LangIdx")
If oSettings.Exists("[Options]RunOnStartup") Then RunOnStartup = (LCase(oSettings.Item("[Options]RunOnStartup"))="true")
If oSettings.Exists("[Options]BringToFront") Then BringToFront = (LCase(oSettings.Item("[Options]BringToFront"))="true")
If oSettings.Exists("[Options]Stretch") Then Stretch = (LCase(oSettings.Item("[Options]Stretch"))="true")
If oSettings.Exists("[Options]ShowFileName") Then ShowFileName = (LCase(oSettings.Item("[Options]ShowFileName"))="true")
If oSettings.Exists("[Options]PicDelay") Then PicDelay = oSettings.Item("[Options]PicDelay")
If oSettings.Exists("[Options]MaxScrPct") Then MaxScrPct = oSettings.Item("[Options]MaxScrPct")
If oSettings.Exists("[Options]Coordinates") Then Coordinates = oSettings.Item("[Options]Coordinates")
If Stretch="" Then Stretch = False
If PicDelay="" Then PicDelay = 5
If MaxScrPct="" Then MaxScrPct = 25

Msg = Split(L(LangIdx),"|")

oWSH.Run "Cmd /c dir /b *.png *.jpg *.bmp *.gif>" & Q & PicListFile & Q,0,True
FileContents = ""
On Error Resume Next
Set oFile = oFSO.OpenTextFile(PicListFile,ForReading)
FileContents = oFile.ReadAll
oFile.Close
On Error Goto 0

If FileContents="" Then
  MsgBox Msg(12) & Z & Z & MyFolder,vbOKonly,Msg(13)
  self.close
Else
  ArrPic = Split(FileContents,Z)
End If

'End Main


Sub Ini2Dict
  If oFSO.FileExists(INIFile) Then 
    Set oFile = oFSO.OpenTextFile(INIFile,ForReading)
    Do Until oFile.AtEndOfStream
      Line = Trim(oFile.ReadLine)
      If Line<>"" And Left(Line,1)<>";" Then
        If Left(Line,1)="[" Then
          Section = Line
        Else
          ArrLine = Split(Line,"=")
          If UBound(Arrline)=1 Then oSettings.Add Section & ArrLine(0),ArrLine(1)
        End If
      End If
    Loop
    oFile.Close
  End If
End Sub

Sub window_onLoad

  If OptionMode Then
    document.body.style.backgroundPosition="0px -" & CaptionHeight & "px"
    ShowOptions
    If LangIdx<>0 Then UpdateLang
  Else
    HideOptions
  End If
  
  If NTVer<"6.1" Then
    Bsw.Disabled = True
    Brw.Disabled = True
  End If
  
  Document.Title = MyTitle
  If Not BringToFront Then Window.blur

  LoadPic

  If Coordinates<>"" Then RepositionWindow

  ToggleShortcut

  wTpp = Window.outerWidth: hTpp = Window.outerHeight

  If OptionMode Then
    UpdatePos = Window.SetInterval("UpdatePosition()", 100)
  Else
    If UBound(ArrPic)>1 Then PicTimer = Window.SetInterval("LoadPic()", PicDelay*1000)
  End If

End Sub

Sub UpdateLang
  Msg = Split(L(LangIdx),"|")
  M0.innerHTML = Msg(0)
  M1.innerHTML = Msg(1)
  M2.innerHTML = Msg(2)
  M3.innerHTML = Msg(3)
  M4.innerHTML = Msg(4)
  M5.innerHTML = Msg(5)
  M6.innerHTML = Msg(6)
  M7.innerHTML = Msg(7)
  M8.innerHTML = Msg(21)
  Bsw.Value = Msg(8)
  Brw.Value = Msg(9)
  Bcn.Value = Msg(10)
  Bhp.Value = Msg(11)
End Sub

Sub UpdateXY
  x = 0: y = 0
  aCoords = Split(Coordinates,",")
  On Error Resume Next
  x = CInt(aCoords(0))
  y = CInt(aCoords(1))
  On Error Goto 0
  Coordinates = x & "," & y
End Sub

Sub RepositionWindow
  UpdateXY
  Window.MoveTo x - BorderSize, y
End Sub

Sub ToggleShortcut
  Crs.blur
  RunOnstartup = Crs.Checked
  ShortCutFolder = oWSH.SpecialFolders("Startup")
  ShortcutExists = False
  Set oFolder = oFSO.GetFolder(ShortCutFolder)
  For Each oFile In oFolder.Files
    If LCase(oFSO.GetExtensionName(oFile.Name)) = "lnk" Then
      Set link = oWSH.CreateShortcut(ShortCutFolder & "\" & oFile.Name)
      If link.TargetPath=PicPath & HTAName Then
        ShortcutExists = True
        If Not RunOnstartup Then oFSO.DeleteFile ShortCutFolder & "\" & oFile.Name
      End If
    End If
  Next
  If RunOnstartup And Not ShortcutExists Then
    ShortCutPath = ShortCutFolder & "\" & MyTitle & Replace(Replace(Replace(Now(),"/",""),":","")," ","") & ".lnk"
    Set link = oWSH.CreateShortcut(ShortCutPath)
    link.TargetPath = PicPath & HTAName
    link.Save
  End If
End Sub

Sub UpdatePosition
  sw = screen.availWidth
  sh = screen.availHeight
  w = Window.outerWidth
  h = Window.outerHeight
  UpdateTpp = Not(w=wTpp And h=hTpp)
  w = w - (BorderSize * 2)
  h = h - BorderSize
  If UpdateTpp Then
    NewSize = Cint((w * h) / (sw * sh) * 100)
    If NewSize>100 Then NewSize = 100
    If NewSize<=0 Then NewSize = 1
    Tpp.Value = NewSize
    MaxScrPct = Tpp.Value
  Else
    Tpp.Value = MaxScrPct
  End If
  Tsp.Value = window.screenX + BorderSize & "," & window.screenY
  Coordinates = Tsp.Value
  UpdateXY
  If Mpp.SelectedIndex=0 And Tsp.Value<>CenterCoordinates Then
    Mpp.SelectedIndex=1
    Tsp.style.display = ""
  End If
End Sub

Sub LoadPic
  If Forward Then PicIndex = PicIndex + 1 Else PicIndex = PicIndex - 1
  If PicIndex=UBound(ArrPic) Then PicIndex = 0
  If PicIndex<0 Then PicIndex = UBound(ArrPic)-1
  aImage = Split(ArrPic(PicIndex),"|")
  If UBound(aImage)=0 Then
    On Error Resume Next
    oImg.LoadFile aImage(0)
    If Err.Number=0 Then IsImage=True Else IsImage=False
    On Error Goto 0
    If IsImage Then
      iw = oImg.Width: ih = oImg.Height
      ArrPic(PicIndex) = aImage(0) & "|" & iw & "|" & ih
    Else
      ArrPic(PicIndex) = aImage(0) & "|0|0"
    End If
    aImage = Split(ArrPic(PicIndex),"|")
  End If
  iw = aImage(1): ih = aImage(2)
  Nam.innerHTML = aImage(0)
  If iw>0 And ih>0 Then
    BadImage = False
    If ShowFileName Then
      Nam.style.display = ""
    Else
      Nam.style.display = "none"
    End If
    ResizePic
    ResizeWindow
    If Coordinates="" Then CenterWindow
    window.document.body.background = PicPath & aImage(0)
  Else
    BadImage = True
    Nam.style.display = ""
    iw = 9: ih = 6
    ResizePic
    ResizeWindow
    If Coordinates="" Then CenterWindow
    window.document.body.background = ""
  End If
End Sub

Sub ResizePic
  sw = screen.availWidth
  sh = screen.availHeight
  MaxArea = sw * sh * (MaxScrPct/100)
  ImgArea = iw * ih
  w = iw: h = ih
  If ImgArea>MaxArea Or Stretch Or OptionMode Or BadImage Then
    ResizeFactor = Sqr(MaxArea/ImgArea)
    w = iw * ResizeFactor
    h = ih * ResizeFactor
    If w>sw Then h = sw/w * h : w = sw
    If h>sh Then w = sh/h * w : h = sh
    w = CInt(w): h = CInt(h)
    h = h + BorderSize: w = w + (BorderSize * 2)
  End If
End Sub

Sub ResizeWindow
	Window.ResizeTo w, h
End Sub

Sub CenterWindow
  sw = screen.availWidth
  sh = screen.availHeight
	Window.MoveTo (sw - w)/2, (sh - h)/2
  CenterCoordinates = window.screenX + BorderSize & "," & window.screenY
End Sub

Sub ShowOptions
  Opt.style.display = ""
  Crs.Style.Zoom = 1.2
  Cbf.Style.Zoom = 1.2
  Csp.Style.Zoom = 1.2
  Cfn.Style.Zoom = 1.2
  Crs.Checked = RunOnStartup
  Cbf.Checked = BringToFront
  Csp.Checked = Stretch
  Cfn.Checked = ShowFileName
  Tpd.Value = PicDelay
  Tpp.Value = MaxScrPct
  Tsp.Value = Coordinates
  If Coordinates="" Then
    Tsp.style.display = "none"
  Else
    mpp.SelectedIndex = 1
    Tsp.style.display = ""
  End If
  Mln.SelectedIndex = LangIdx
End Sub

Sub HideOptions
  Opt.style.display = "none"
  Tsp.style.display = "none"
End Sub

Sub document_onclick
  If OptionMode And Not DoingInput Then
    ResizePic
    ResizeWindow
    Var2Ini
  End If
End Sub

Sub ManualAdvance(Flag)
  If Not DoingInput Then
    window.event.keyCode = 0
    clearInterval(PicTimer)
    Forward = Flag
    If PicTimer=0 Then LoadPic
    PicTimer = 0
  End If
End Sub

Sub document_onKeyDown
  If window.event.ctrlKey And window.event.keyCode=116 Then
    window.event.keyCode = 0
    Exit Sub
  End If
  If window.event.ctrlKey Or window.event.altKey Or window.event.shiftKey Then Exit Sub
  Select Case window.event.keyCode
    Case 32, 39 'Spacebar, Right arrow
      ManualAdvance(True)
    Case 8, 37 'Backspace, Left arrow
      ManualAdvance(False)
    Case 13 'Enter
      ContinueShow
    Case 27 'Esc
      If Not OptionMode Then LaunchOptionModeHTA
    Case 116 'F5
      window.event.keyCode = 0
    Case 114 'F3
      If OptionMode Then
        window.event.keyCode = 0
        oWSH.Run "Explorer.exe """ & PicPath & """",1,False
      End If
    Case 115 'F4
      If OptionMode Then
        window.event.keyCode = 0
        oWSH.Run "Explorer.exe """ & MyAppData & """",1,False
      End If
  End Select
End Sub

Sub ContinueShow
  If OptionMode Then
    oWSH.Run Q & PicPath & HTAName & Q & " " & Q & PicIndex & Q,1,False
    self.close
  Else
    HideOptions
    Forward = True
    clearInterval(PicTimer)
    PicTimer = Window.SetInterval("LoadPic()", PicDelay*1000)
  End If
End Sub

Sub Help
  Bhp.blur
  oWSH.Run "https://lesferch.github.io/DesktopPic"
End Sub

Sub LaunchOptionModeHTA
  clearInterval(PicTimer)
  If oFSO.FileExists(OptionModeHTA) Then
    If DevMode Then
      oFSO.DeleteFile(OptionModeHTA)
    Else
      Set oFile = oFSO.OpenTextFile(OptionModeHTA,ForReading)
      FileContents = oFile.ReadAll
      oFile.Close
      If Instr(FileContents,Q & MyVerNum & Q)=0 Then oFSO.DeleteFile(OptionModeHTA)
    End If
  End If  
  If Not oFSO.FileExists(OptionModeHTA) Then
    Set oFile = oFSO.OpenTextFile(MyPath,ForReading)
    MyCode = oFile.ReadAll
    oFile.Close
    MyCode = Replace(MyCode,LCase("CAPTION=NO"),"caption=yes")
    MyCode = Replace(MyCode,LCase("BORDER=NONE"),"border=thick")
    MyCode = Replace(MyCode,LCase("SCROLL=NO"),"scroll=auto")
    Set oFile = oFSO.OpenTextFile(OptionModeHTA,ForWriting,True)
    oFile.Write MyCode
    oFile.Close
  End If
  oWSH.Run Q & OptionModeHTA & Q & " " & Q & PicIndex & Q & " " & Q & MyFolder & Q & " " & Q & MyName & Q,1,False
  self.close
End Sub

Sub OnFocusInput
  DoingInput = True
  ClearInterval(UpdatePos)
End Sub

Sub OnBlurInput
  DoingInput = False
  UpdatePos = Window.SetInterval("UpdatePosition()", 500)
End Sub

Sub OnInputDigits(id)
  document.getElementByID(id).Value = DigitsOnly.Replace(document.getElementByID(id).Value,"")
End Sub

Sub OnInputDigitsComma(id)
  document.getElementByID(id).Value = DigitsComma.Replace(document.getElementByID(id).Value,"")
  document.getElementByID(id).Value = Replace(document.getElementByID(id).Value,",,",",")
End Sub

Sub OnChangeTpd
  If Tpd.Value<1 Then Tpd.Value = 1
End Sub

Sub OnChangeTpp
  If Tpp.Value<1 Or Tpp.Value>100 Then
    Tpp.Value = MaxScrPct
  Else
    MaxScrPct = Tpp.Value
  End If
  ResizePic
  ResizeWindow
  wTpp = Window.outerWidth: hTpp = Window.outerHeight
End Sub

Sub OnChangeMPP
  Mpp.blur
  If Mpp.SelectedIndex=1 Then
    Tsp.style.display = ""
  Else
    CenterWindow
    Tsp.style.display = "none"
  End If
End Sub

Sub OnChangeMln
  LangIdx = Mln.SelectedIndex
  UpdateLang
End Sub

Sub OnChangeTsp
  If tsp.Value<>"" Then 
    Coordinates = Tsp.Value
    RepositionWindow
  End If
End Sub

Sub OnClickCfn
  Cfn.blur
  If Cfn.checked Then
    Nam.style.display = ""
    ShowFileName = True
  Else
    Nam.style.display = "none"
    ShowFileName = False
  End If
End Sub

Sub window_onUnload
  If OptionMode Then Var2Ini
End Sub

Sub Var2Ini
  Set oFile = oFSO.OpenTextFile(INIFile,ForWriting,True)
  oFile.Write "[Options]" & Z
  oFile.Write "LangIdx=" & LangIdx & Z
  oFile.Write "RunOnStartup=" & Crs.Checked & Z
  oFile.Write "BringToFront=" & Cbf.Checked & Z
  oFile.Write "Stretch=" & Csp.Checked & Z
  oFile.Write "ShowFileName=" & Cfn.Checked & Z
  oFile.Write "PicDelay=" & Tpd.Value & Z
  oFile.Write "MaxScrPct=" & Tpp.Value & Z
  If Mpp.SelectedIndex=0 Then Tsp.Value=""
  If InStr(Tsp.Value,HideMe) Then Tsp.Value = ""
  oFile.Write "Coordinates=" & Tsp.Value & Z
  oFile.Close
End Sub

Sub RestoreWallpaper
  Brw.blur

  If (x<0 Or y<0 Or x>screen.Width Or y>screen.Height) Then
    MsgBox Msg(14) & Z & Z & Msg(15),vbOKonly,Msg(9)
    Exit Sub
  End If

  oWSH.Run Q & WallPExe & Q & " 0",0,True
  Monitor = oWSH.RegRead("HKCU\Software\WallP\Monitor")
  WPpath = oWSH.RegRead("HKCU\Software\WallP\WPpath")

  On Error Resume Next
  WPbackup = oWSH.RegRead("HKCU\Software\WallP\" & Monitor & "\WPbackup")
  On Error Goto 0
  If oFSO.FileExists(WPbackup) Then
    oWSH.Run Q & WallPExe & Q & " 0 " & Q & WPbackup & Q,0,True
  Else
    On Error Resume Next
    WPbackup = oWSH.RegRead("HKCU\Software\WallP\All\WPbackup")
    On Error Goto 0
    If oFSO.FileExists(WPbackup) Then
      oWSH.Run Q & WallPExe & Q & " 0 " & Q & WPbackup & Q,0,True
    End If
  End If

  If oFSO.FileExists(WPpath) Then
    If InStr(LCase(WPpath),LCase(MyAppData & "DesktopPic"))=0 Then 
      oWSH.RegWrite "HKCU\Software\WallP\" & Monitor & "\WPbackup",WPpath
      oWSH.RegWrite "HKCU\Software\WallP\All\WPbackup",WPpath
    End If
  End If

End Sub

Sub StampWallpaper
  Bsw.blur

  If (x<0 Or y<0 Or x>screen.Width Or y>screen.Height) Then
    MsgBox Msg(14) & Z & Z & Msg(15),vbOKonly,Msg(8)
    Exit Sub
  End If

  'Check for Fill style
  WallpaperStyle = oWSH.RegRead("HKCU\Control Panel\Desktop\WallpaperStyle")
  If WallpaperStyle<>"10" Then
    Answer = MsgBox(Msg(16) & Z & Z & Msg(17),vbYesNo,Msg(8))
    If Answer = vbNo Then Exit Sub
  End If

  'Detect if slideshow is enabled
  BackgroundType = 0
  On Error Resume Next
  BackgroundType = oWSH.RegRead("HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\Wallpapers\BackgroundType")
  On Error Goto 0
  If BackgroundType=2 Then
    Answer = MsgBox(Msg(19) & Z & Z & Msg(20),vbYesNo,Msg(8))
    If Answer = vbNo Then Exit Sub
  End If
  
  If NTVer="6.1" Then
    If oWSH.RegRead("HKCU\Control Panel\Desktop\Wallpaper")="" Then BackgroundType = 1
  End If
  
  sw = screen.availWidth
  sh = screen.availHeight
  Fsw = screen.Width
  Fsh = screen.Height

  StampImg = PicPath & aImage(0)
  oWSH.Run Q & WallPExe & Q & " 0",0,True
  Monitor = oWSH.RegRead("HKCU\Software\WallP\Monitor")
  WPpath = oWSH.RegRead("HKCU\Software\WallP\WPpath")
  StampedFile = MyAppData & "DesktopPic" & Monitor & PicExt

  'Solid color background
  If BackgroundType=1 Then
    Colors = Split(oWSH.RegRead("HKCU\Control Panel\Colors\Background")," ")
    c = CLng("&hFF" & Right("0" & Hex(Colors(0)),2) & Right("0" & Hex(Colors(1)),2) & Right("0" & Hex(Colors(2)),2))

    Pad = Int(0.1 * Math.Max(Fsw,Fsh))

    Set v = CreateObject("WIA.Vector")
    For i = 1 To 400
      v.Add c
    Next
    Set Img = v.ImageFile(20,20)
    Set IP = CreateObject("WIA.ImageProcess")
    IP.Filters.Add IP.FilterInfos("Scale").FilterID
    IP.Filters(1).Properties("MaximumWidth") = Fsw + Pad
    IP.Filters(1).Properties("MaximumHeight") = Fsh + Pad
    IP.Filters(1).Properties("PreserveAspectRatio") = KeepAspect
    IP.Filters.Add IP.FilterInfos("Crop").FilterID
    IP.Filters(2).Properties("Right") = Pad
    IP.Filters(2).Properties("Bottom") = Pad
    IP.Filters.Add IP.FilterInfos("Convert").FilterID
    IP.Filters(3).Properties("FormatID").Value = PicFormat
    Set Img = IP.Apply(Img)
    
    SolidColorImage = MyAppData & "SolidColorImage" & PicExt

    If oFSO.FileExists(SolidColorImage) Then oFSO.DeleteFile SolidColorImage
    Img.SaveFile SolidColorImage
    Set Img=Nothing
    Set IP=Nothing

    WPpath = SolidColorImage

    oWSH.Run Q & WallPExe & Q & " 0 Fill " & Q & SolidColorImage & Q,0,True
    MonitorCount = oWSH.RegRead("HKCU\Software\WallP\MonitorCount")
    If MonitorCount>1 Then
      For i = 1 To MonitorCount-1
        oWSH.Run Q & WallPExe & Q & " " & i & " Fill " & Q & SolidColorImage & Q,0,True
      Next
    End If

  End If


  If oFSO.FileExists(WPpath) Then
    If InStr(LCase(WPpath),LCase(MyAppData & "DesktopPic"))=0 Then 
      oWSH.RegWrite "HKCU\Software\WallP\" & Monitor & "\WPbackup",WPpath
      oWSH.RegWrite "HKCU\Software\WallP\All\WPbackup",WPpath
    End If
  Else
    MsgBox Msg(18) & Z & Z & WPpath
    Exit Sub
  End If
   
  'Scale image to be stamped
  Set Img = CreateObject("WIA.ImageFile")
  Img.LoadFile StampImg
  Set IP = CreateObject("WIA.ImageProcess")
  IP.Filters.Add IP.FilterInfos("Scale").FilterID
  IP.Filters(1).Properties("MaximumWidth") = w
  IP.Filters(1).Properties("MaximumHeight") = h
  Set Img = IP.Apply(Img)
  Set IP=Nothing

  'Load current wallpaper
  Set Wall = CreateObject("WIA.ImageFile")
  Wall.LoadFile WPpath

  'Adjust current wallpaper to fill screen
  If Not(Wall.Width=Fsw And Wall.Height=Fsh) Then
  
    wFactor = Fsw / Wall.Width
    hFactor = Fsh / Wall.Height
    If wFactor>hFactor Then NewWidth = Fsw : NewHeight = Int(wFactor * Wall.Height)
    If hFactor>wFactor Then NewHeight = Fsh : NewWidth = Int(hFactor * Wall.Width)
    Set IP = CreateObject("WIA.ImageProcess")
    IP.Filters.Add IP.FilterInfos("Scale").FilterID
    IP.Filters(1).Properties("MaximumWidth") = NewWidth
    IP.Filters(1).Properties("MaximumHeight") = NewHeight
    IP.Filters(1).Properties("PreserveAspectRatio") = KeepAspect
    Set Wall = IP.Apply(Wall)
    Set IP=Nothing
    NewWidth = Wall.Width
    NewHeight = Wall.Height

    'Crop excess width or height
    If NewWidth>Fsw Or NewHeight>Fsh Then
      LeftCrop = 0
      RightCrop = 0
      TopCrop = 0
      BotCrop = 0
      If NewWidth>Fsw Then
        LeftCrop = Int((NewWidth - Fsw) / 2)
        RightCrop = NewWidth - Fsw - LeftCrop
      End If
      If NewHeight>Fsh Then
        TopCrop = Int((NewHeight - Fsh) / 2)
        TopCrop = Int(TopCrop * 2 / 3)
        BotCrop = NewHeight - Fsh - TopCrop
      End If

      Set IP = CreateObject("WIA.ImageProcess")
      IP.Filters.Add IP.FilterInfos("Crop").FilterID
      IP.Filters(1).Properties("Left") = LeftCrop
      IP.Filters(1).Properties("Right") = RightCrop
      IP.Filters(1).Properties("Top") = TopCrop
      IP.Filters(1).Properties("Bottom") = BotCrop
      Set Wall = IP.Apply(Wall)
      Set IP=Nothing

    End If
    
  End If

  'Stamp image
  Set IP = CreateObject("WIA.ImageProcess")
  IP.Filters.Add IP.FilterInfos("Stamp").FilterID
  Set IP.Filters(1).Properties("ImageFile") = Img
  IP.Filters(1).Properties("Left") = window.screenX + BorderSize
  IP.Filters(1).Properties("Top") = window.screenY
  IP.Filters.Add IP.FilterInfos("Convert").FilterID
  IP.Filters(2).Properties("FormatID").Value = PicFormat
  Set Wall = IP.Apply(Wall)
  If oFSO.FileExists(StampedFile) Then oFSO.DeleteFile StampedFile
  Wall.SaveFile StampedFile
  Set Wall=Nothing
  Set Img=Nothing
  Set IP=Nothing

  oWSH.Run Q & WallPExe & Q & " 0 Fill " & Q & StampedFile & Q,0,True
  
End Sub

</script>
<style>
body {background-size:cover; background-attachment:fixed; background-repeat:no-repeat}
br {font-size: 16pt}
.cc {
font-family: sans-serif;
font-weight: normal;
font-size: 11pt;
color: white;
background: rgba(0,0,0,.4);
padding: 1pt;
border-radius: 2pt;
}
</style>
</head>
<body>
<span id=Nam class=cc></span>
<div id=Opt>
<input type=button id=Bsw Value=Stamp onclick=StampWallpaper()>
<input type=button id=Brw Value=Restore onclick=RestoreWallpaper()>
<input type=button id=Bcn Value=Continue onclick=ContinueShow()>
<input type=button id=Bhp Value=Help onclick=Help()>
<br>
<select id=Mln  OnChange=OnChangeMln()>
<option>English</option>
<option>Deutsch</option>
<option>Español</option>
<option>Français</option>
<option>日本語</option>
<option>中文 (简体)</option>
<option>中文 (繁體)</option>
<option>한국어</option>
<option>Tiếng Việt</option>
</select><br>
<input type=checkbox id=Crs OnClick=ToggleShortcut()>
<span id=M0 class=cc>Run on startup</span><br>
<input type=checkbox id=Cbf OnClick=Cbf.blur()>
<span id=M1 class=cc>Bring to front on start</span><br>
<input type=checkbox id=Csp OnClick=Csp.blur()>
<span id=M2 class=cc>Stretch smaller pictures</span><br>
<input type=checkbox id=Cfn OnClick=OnClickCfn()>
<span id=M8 class=cc>Display file names</span><br>
<input type=text size=5 id=Tpd MaxLength=5 OnFocus=OnFocusInput() OnBlur=OnBlurInput() OnInput=OnInputDigits("Tpd") OnChange=OnChangeTpd()>
<span id=M3 class=cc>Pic delay in seconds</span><br>
<input type=text size=5 id=Tpp MaxLength=3 OnFocus=OnFocusInput() OnBlur=OnBlurInput() OnInput=OnInputDigits("Tpp") OnChange=OnChangeTpp()>
<span id=M4 class=cc>Max percent of screen area</span><br>
<select id=Mpp  OnChange=OnChangeMpp()>
<option id=M5>Centered</option>
<option id=M6>Custom</option>
</select>
<span id=M7 class=cc>Window position</span>
<input type=text size=11 id=Tsp MaxLength=11 OnFocus=OnFocusInput() OnBlur=OnBlurInput() OnInput=OnInputDigitsComma("Tsp") OnChange=OnChangeTsp()>
<br>
</div>
</body>
</html>
<!--
This is a base64 encoding of WallP.exe
-----BEGIN CERTIFICATE-----
TVqQAAMAAAAEAAAA//8AALgAAAAAAAAAQAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAgAAAAA4fug4AtAnNIbgBTM0hVGhpcyBwcm9ncmFtIGNhbm5v
dCBiZSBydW4gaW4gRE9TIG1vZGUuDQ0KJAAAAAAAAABQRQAATAEDAJWEDf8AAAAA
AAAAAOAAIgALATAAAB4AAAAIAAAAAAAAtjwAAAAgAAAAQAAAAABAAAAgAAAAAgAA
BAAAAAAAAAAGAAAAAAAAAACAAAAAAgAAAAAAAAMAYIUAABAAABAAAAAAEAAAEAAA
AAAAABAAAAAAAAAAAAAAAGQ8AABPAAAAAEAAAIwFAAAAAAAAAAAAAAAAAAAAAAAA
AGAAAAwAAADUOwAAOAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAIAAACAAAAAAAAAAAAAAACCAAAEgAAAAAAAAAAAAAAC50ZXh0AAAA
vBwAAAAgAAAAHgAAAAIAAAAAAAAAAAAAAAAAACAAAGAucnNyYwAAAIwFAAAAQAAA
AAYAAAAgAAAAAAAAAAAAAAAAAABAAABALnJlbG9jAAAMAAAAAGAAAAACAAAAJgAA
AAAAAAAAAAAAAAAAQAAAQgAAAAAAAAAAAAAAAAAAAACYPAAAAAAAAEgAAAACAAUA
yCUAAAwWAAADAAIAAQAABgAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAABswBABmBAAAAQAAEQByAQAAcAog/8maOwsfYw1yAQAA
cBMEcgMAAHATBRcTBgKOFv4BEwcRBzmWAAAAAHILAABwKBAAAAoAclkAAHAoEAAA
CgByuQAAcCgQAAAKAHIpAQBwKBAAAAoAcsQBAHAoEAAACgByFgIAcCgQAAAKAHJg
AgBwKBAAAAoAcsQCAHAoEAAACgBySwMAcCgQAAAKAHLgAwBwKBAAAAoAclQEAHAo
EAAACgBy6QQAcCgQAAAKAHJTBQBwKBAAAAoAADigAwAAABYTCzj8AAAAAAIRC5pv
EQAACnLiBQBwKBIAAAoTDBEMLAQAFg0AAhELmm8RAAAKcvAFAHAoEgAAChMNEQ0s
BAAXDQACEQuabxEAAApy+gUAcCgSAAAKEw4RDiwEABgNAAIRC5pvEQAACnIKBgBw
KBIAAAoTDxEPLAQAGQ0AAhELmm8RAAAKchIGAHAoEgAAChMQERAsBAAaDQACEQua
bxEAAApyHAYAcCgSAAAKExERESwEABsNAAIRC5ooEwAAChMSERIsBwACEQuaCgAA
AAIRC5ooFAAACgsA3gUmAADeAAIRC5pyJgYAcG8VAAAKExMREywIAAIRC5oTBAAA
EQsXWBMLEQsCjmn+BBMUERQ69P7//3IqBgBwcqoGAHByyAYAcCgWAAAKdBcAAAET
CBEIKBcAAApy0AYAcHMYAAAKKBkAAAoTFREVLBEActgGAHAoEAAACgA4QwIAABEI
ctAGAHAoEgAAChMJfhoAAApyGgcAcG8bAAAKEwoGcgEAAHAoHAAAChMWERYsGAAo
HQAACm8eAAAKBigfAAAKKCAAAAoKABEJExcRFyx7AAZyAQAAcCgcAAAKExgRGCxJ
AHIsBwBwExkRGXJ8BwBwBhcoIQAACgARGXKQBwBwcq4HAHAXKCEAAAoAERlytAcA
cHLQBwBwFyghAAAKAB8UFgYYKAIAAAYmAHIsBwBwcnwHAHByAQAAcCgWAAAKdBcA
AAEKADjXAAAAAHMRAAAGdAQAAAITGhEabwgAAAYTBgcRBjcNByD/yZo7/gEW/gEr
ARYTGxEbLAYAOEMBAAAAABEaB28HAAAGDADeCyYAcgEAAHAMAN4AAAAICHLUBwBw
byIAAAofC28jAAAKEwUA3gwmAHIDAABwEwUA3gAGcgEAAHAoHAAAChMcERwsDAAR
GggGbwUAAAYAABEaCG8GAAAGCgkfY/4BFv4BEx0RHSwLABEaCW8MAAAGAAARBHIB
AABwKBwAAAoTHhEeLBEAERoRBCgDAAAGbwoAAAYAAAARCnLcBwBwbxsAAAoTHwAR
H3LoBwBwEQaMHwAAAW8kAAAKABEfcgIIAHARBW8kAAAKABEfchIIAHAGbyQAAAoA
ER8RBW8bAAAKEyAAESByEggAcAZvJAAACgAA3g0RICwIESBvJQAACgDcciAIAHBy
wQgAcBaMIQAAARooIQAACgAA3g0RHywIER9vJQAACgDcEQUoEAAACgAGKBAAAAoA
ACoAAAFAAAAAAJQBDqIBBRAAAAEAACMDDTADCxAAAAEAADwDGVUDDBAAAAECAA8E
EiEEDQAAAAACANIDdkgEDQAAAAAbMAUAggAAAAIAABEAFgoWCxYMAheNIgAAASUW
HyydbyYAAAoNAAkWmigUAAAKCgDeBSYAAN4AAAAJF5ooFAAACgsA3gUmAADeAAAA
CRiaKBQAAAoMAN4FJgAA3gAWBtIH0gjSKCcAAAoTBBIEKCgAAAoSBCgpAAAKHmJg
EgQoKgAACh8QYmATBSsAEQUqAAABKAAAAAAZAA0mAAUQAAABAAAsAA05AAUQAAAB
AAA/AA1MAAUQAAABIgIoKwAACgAqAAAAQlNKQgEAAQAAAAAADAAAAHY0LjAuMzAz
MTkAAAAABQBsAAAA4AQAACN+AABMBQAAbAUAACNTdHJpbmdzAAAAALgKAADgCAAA
I1VTAJgTAAAQAAAAI0dVSUQAAACoEwAAZAIAACNCbG9iAAAAAAAAAAIAAAFXNQIU
CQIAAAD6ATMAFgAAAQAAACIAAAAFAAAABAAAABEAAAAUAAAAKwAAABEAAAAMAAAA
AgAAAAEAAAABAAAAAQAAAAIAAAADAAAAAAByAgEAAAAAAAYA1QE+BAYAQgI+BAYA
8gAMBA8AXgQAAAYAGgFAAwYAuAFAAwYAmQFAAwYAKQJAAwYA9QFAAwYADgJAAwYA
SAFAAwYABgEfBAYA5AAfBAYAfAFAAwYAYwGEAgYAugT1AgYAKAUBAAoA+gOvAgYA
zAD1AgYAuwAfBAYAMQEfBAYAoQD1AgYAngL1AgYAnABAAAYA7AT1AgYARgUBAAYA
MAP1AgYAFAP1AgYAxQJAAAYAdwABAAYAEwD1AgYAkAD1AgYAFAD1AgYAjAP1AgAA
AAAaAAAAAAABAAEAAAAQAEoAOQBBAAEAAQAKARAAtQQAAE0AAQAFAKIQAACRAwAA
AAAFAAUAAhAQAIEEAABBAAUAEQAGAMEE6AAGAIgD6AAGAMYE6AAGAPwC6ABQIAAA
AACRAA8D6wABAAAAAACAAJYgcwPxAAIABCUAAAAAkQD3A1wABgC8JQAAAACGGAYE
BgAHAAAAAAAAAMYFsAP5AAcAAAAAAAAAxgWjA/8ACQAAAAAAAADGBZ4EBAELAAAA
AAAAAMYF0gQJAQ0AAAAAAAAAxgVWAA0BDgAAAAAAAADGBeQDEwEQAAAAAAAAAMYF
0QMJAREAAAAAAAAAxgVeAwEAEgAAAAAAAADGBVIDTQATAAAAAAAAAMYFAQUYARQA
AAAAAAAAxgX0BB0BFQAAAAAAAADGBYkAIQEVAAAAAAADEIYYBgQGABUAAAABAG0E
AAABADgDAAACAOUCAAADAOwCAAAEANECAAABAGoAACABACkAACACAMoCACAAAAAA
ACABACkAACAAAAAAAAABAA4FACAAAAAAACAAAAAAACABACkAACABAAAEACAAAAAA
ACABAGoDACAAAAAAAAABAHIECQAGBAEAEQAGBAYAGQAGBAoAKQAGBBAAMQAGBBAA
OQAGBBAAQQAGBBAASQAGBBAAUQAGBBAAWQAGBBAAYQAGBBUAaQAGBBAAcQAGBBAA
eQAGBBAAqQAGBBoAsQCpAEgAuQDJA00AuQBPBVEAwQCXBFcAyQARAFwAuQB4BGEA
0QBgAmYA2QDeAG0A2QAGBBAA2QADA3MA0QC9A3sAiQAbBX8AuQBbBVEA4QAeA4UA
4QA0BU0A6QCzAIoA6QC+ApAA0QBpApUAuQB8Ap4AuQClAqMAiQBpAqkAAQHWAAYA
uQDMBLoAkQBlAMEAkQBQAMoAkQAzAMoAkQAjAMoAgQAGBAYALgALACUBLgATAC4B
LgAbAE0BLgAjAFYBLgArAGEBLgAzAGEBLgA7AGEBLgBDAFYBLgBLAGcBLgBTAGEB
LgBbAGEBLgBjAH8BLgBrAKkBLgBzALYBgwBjAAQCgwB7AC4CowBjADcCDwDgABEA
4AATAOAAFQDgABcA4AAbAOIAHQDkAB8A4AAhAOIAIwDiACUA5gAnAOYAIACvANoC
AAEFAHMDAQAEgAAAAQAAAAAAAAAAAAAAAABKAAAABAAAAAAAAAAAAAAAzgBuAAAA
AAAEAAAAAAAAAAAAAADXAK8CAAAAAAMAAgAEAAIABQACAAAAAE1pY3Jvc29mdC5X
aW4zMgBUb1VJbnQzMgA8TW9kdWxlPgBnZXRfQgBtb25pdG9ySUQAZ2V0X0cAV2lu
QVBJAFN5c3RlbS5JTwBXYWxsUABnZXRfUgBHZXRNb25pdG9yUkVDVABGcm9tQXJn
YgBtc2NvcmxpYgBSZWdpc3RyeVZhbHVlS2luZABFbmFibGUASURpc3Bvc2FibGUA
RmlsZQBDb25zb2xlAFdyaXRlTGluZQBDb21iaW5lAENvbUludGVyZmFjZVR5cGUA
VmFsdWVUeXBlAERpc3Bvc2UAUGFyc2UAR3VpZEF0dHJpYnV0ZQBEZWJ1Z2dhYmxl
QXR0cmlidXRlAENvbVZpc2libGVBdHRyaWJ1dGUAQXNzZW1ibHlUaXRsZUF0dHJp
YnV0ZQBJbnRlcmZhY2VUeXBlQXR0cmlidXRlAEFzc2VtYmx5VHJhZGVtYXJrQXR0
cmlidXRlAFRhcmdldEZyYW1ld29ya0F0dHJpYnV0ZQBBc3NlbWJseUZpbGVWZXJz
aW9uQXR0cmlidXRlAEFzc2VtYmx5Q29uZmlndXJhdGlvbkF0dHJpYnV0ZQBBc3Nl
bWJseURlc2NyaXB0aW9uQXR0cmlidXRlAENvbXBpbGF0aW9uUmVsYXhhdGlvbnNB
dHRyaWJ1dGUAQXNzZW1ibHlQcm9kdWN0QXR0cmlidXRlAEFzc2VtYmx5Q29weXJp
Z2h0QXR0cmlidXRlAEFzc2VtYmx5Q29tcGFueUF0dHJpYnV0ZQBSdW50aW1lQ29t
cGF0aWJpbGl0eUF0dHJpYnV0ZQBHZXRWYWx1ZQBTZXRWYWx1ZQBXYWxsUC5leGUA
SW5kZXhPZgBTeXN0ZW0uUnVudGltZS5WZXJzaW9uaW5nAFN0cmluZwBTdWJzdHJp
bmcAU3lzdGVtLkRyYXdpbmcAR2V0RnVsbFBhdGgAV1BwYXRoAGZ1V2luSW5pAHVz
ZXIzMi5kbGwAdVBhcmFtAGxwdlBhcmFtAFN5c3RlbQBCb3R0b20Ab3BfTGVzc1Ro
YW4ATWFpbgBBcHBEb21haW4AZ2V0X0N1cnJlbnREb21haW4AVmVyc2lvbgB1QWN0
aW9uAFN5c3RlbS5SZWZsZWN0aW9uAEdldFBvc2l0aW9uAFNldFBvc2l0aW9uAHBv
c2l0aW9uAFN5c3RlbVBhcmFtZXRlcnNJbmZvAFRvcABDaGFyAElEZXNrdG9wV2Fs
bHBhcGVyAEdldFdhbGxwYXBlcgBTZXRXYWxscGFwZXIAQ3VycmVudFVzZXIAVG9M
b3dlcgBHZXRCYWNrZ3JvdW5kQ29sb3IAU2V0QmFja2dyb3VuZENvbG9yAEludENv
bG9yAGNvbG9yAC5jdG9yAFN5c3RlbS5EaWFnbm9zdGljcwBTeXN0ZW0uUnVudGlt
ZS5JbnRlcm9wU2VydmljZXMAU3lzdGVtLlJ1bnRpbWUuQ29tcGlsZXJTZXJ2aWNl
cwBEZWJ1Z2dpbmdNb2RlcwBhcmdzAGl0ZW1zAENvbnRhaW5zAERlc2t0b3BXYWxs
cGFwZXJDbGFzcwBFeGlzdHMAR2V0TW9uaXRvckRldmljZVBhdGhBdABSZWN0AE9i
amVjdABMZWZ0AFJpZ2h0AFNwbGl0AEdldE1vbml0b3JEZXZpY2VQYXRoQ291bnQA
Q29udmVydABHZXRTbGlkZXNob3cAU2V0U2xpZGVzaG93AG1vbml0b3JJbmRleABD
cmVhdGVTdWJLZXkAUmVnaXN0cnlLZXkAZ2V0X0Jhc2VEaXJlY3RvcnkAUmVnaXN0
cnkAb3BfRXF1YWxpdHkAb3BfSW5lcXVhbGl0eQAAAAAAAQAHQQBsAGwAAE1TAGUA
dAAgAHcAYQBsAGwAcABhAHAAZQByACAAZgBvAHIAIABvAG4AZQAgAG8AcgAgAG0A
bwByAGUAIABtAG8AbgBpAHQAbwByAHMAAF9GAHUAbABsACAAZgB1AG4AYwB0AGkA
bwBuAGEAbABpAHQAeQAgAHIAZQBxAHUAaQByAGUAcwAgAFcAaQBuAGQAbwB3AHMA
IAA4ACAAbwByACAAaABpAGcAaABlAHIAAG9XAGkAbgBkAG8AdwBzACAANwAgAGwA
aQBtAGkAdABlAGQAIAB0AG8AIABzAGUAdAB0AGkAbgBnACAAdwBhAGwAbABwAGEA
cABlAHIAIABmAG8AcgAgAGEAbABsACAAbQBvAG4AaQB0AG8AcgBzAACAmVUAcwBh
AGcAZQA6ACAAVwBhAGwAbABQAC4AZQB4AGUAIABbAE0AbwBuAGkAdABvAHIASQBu
AGQAZQB4AF0AIABbAEkAbQBhAGcAZQBGAGkAbABlAFAAYQB0AGgAXQAgAFsAUABv
AHMAaQB0AGkAbwBuAF0AIABbAEIAYQBjAGsAZwByAG8AdQBuAGQAQwBvAGwAbwBy
AF0AAFFQAGEAcgBhAG0AZQB0AGUAcgBzACAAYwBhAG4AIABiAGUAIABzAHAAZQBj
AGkAZgBpAGUAZAAgAGkAbgAgAGEAbgB5ACAAbwByAGQAZQByAABJTQBvAG4AaQB0
AG8AcgBJAG4AZABlAHgAIABpAHMAIABhACAAegBlAHIAbwAtAGIAYQBzAGUAZAAg
AGkAbgB0AGUAZwBlAHIAAWNJAG0AYQBnAGUARgBpAGwAZQBQAGEAdABoACAAYwBh
AG4AIABiAGUAIABhAG4AIABhAGIAcwBvAGwAdQB0AGUAIABvAHIAIAByAGUAbABh
AHQAaQB2AGUAIABwAGEAdABoAACAhUkAZgAgAE0AbwBuAGkAdABvAHIASQBuAGQA
ZQB4ACAAaQBzACAAbwBtAGkAdAB0AGUAZAAsACAAdwBhAGwAbABwAGEAcABlAHIA
IAB3AGkAbABsACAAYgBlACAAcwBlAHQAIABmAG8AcgAgAGEAbABsACAAbQBvAG4A
aQB0AG8AcgBzAACAk0kAZgAgAEkAbQBhAGcAZQBGAGkAbABlAFAAYQB0AGgAIABp
AHMAIABvAG0AaQB0AHQAZQBkACwAIABNAG8AbgBpAHQAbwByAEkAbgBkAGUAeAAg
AHcAYQBsAGwAcABhAHAAZQByACAAcABhAHQAaAAgAHcAaQBsAGwAIABiAGUAIABy
AGUAdAB1AHIAbgBlAGQAAHNQAG8AcwBpAHQAaQBvAG4AIABjAGEAbgAgAGIAZQAg
AG8AbgBlACAAbwBmADoAIABDAGUAbgB0AGUAcgAgAFQAaQBsAGUAIABTAHQAcgBl
AHQAYwBoACAARgBpAHQAIABGAGkAbABsACAAUwBwAGEAbgAAgJNJAGYAIABQAG8A
cwBpAHQAaQBvAG4AIABpAHMAIABvAG0AaQB0AHQAZQBkACwAIABwAG8AcwBpAHQA
aQBvAG4AIABpAHMAIAB1AG4AYwBoAGEAbgBnAGUAZAAgAGYAbwByACAAQwBlAG4A
dABlAHIAIABTAHQAcgBlAHQAYwBoACAARgBpAHQAIABGAGkAbABsAABpSQBmACAA
UABvAHMAaQB0AGkAbwBuACAAaQBzACAAbwBtAGkAdAB0AGUAZAAsACAAUwBwAGEA
bgAgAGEAbgBkACAAVABpAGwAZQAgAHIAZQB2AGUAcgB0ACAAdABvACAARgBpAGwA
bAAAgI1CAGEAYwBrAGcAcgBvAHUAbgBkAEMAbwBsAG8AcgAgAGkAcwAgAHMAcABl
AGMAaQBmAGkAZQBkACAAYQBzACAAcgAsAGcALABiAC4AIABFAHgAYQBtAHAAbABl
ACAAKABDAG8AbwBsACAAYgBsAHUAZQApADoAIAA0ADUALAAxADIANQAsADEANQA0
AAANYwBlAG4AdABlAHIAAAl0AGkAbABlAAAPcwB0AHIAZQB0AGMAaAAAB2YAaQB0
AAAJZgBpAGwAbAAACXMAcABhAG4AAAMsAAB/SABLAEUAWQBfAEwATwBDAEEATABf
AE0AQQBDAEgASQBOAEUAXABTAG8AZgB0AHcAYQByAGUAXABNAGkAYwByAG8AcwBv
AGYAdABcAFcAaQBuAGQAbwB3AHMAIABOAFQAXABDAHUAcgByAGUAbgB0AFYAZQBy
AHMAaQBvAG4AAB1DAHUAcgByAGUAbgB0AFYAZQByAHMAaQBvAG4AAAc2AC4AMAAA
BzYALgAxAABBVwBpAG4AZABvAHcAcwAgADcAIABvAHIAIABnAHIAZQBhAHQAZQBy
ACAAaQBzACAAcgBlAHEAdQBpAHIAZQBkAAARUwBvAGYAdAB3AGEAcgBlAABPSABL
AEUAWQBfAEMAVQBSAFIARQBOAFQAXwBVAFMARQBSAFwAQwBvAG4AdAByAG8AbAAg
AFAAYQBuAGUAbABcAEQAZQBzAGsAdABvAHAAABNXAGEAbABsAHAAYQBwAGUAcgAA
HVcAYQBsAGwAcABhAHAAZQByAFMAdAB5AGwAZQAABTEAMAAAG1QAaQBsAGUAVwBh
AGwAbABwAGEAcABlAHIAAAMwAAAHVQBJAEQAAAtXAGEAbABsAFAAABlNAG8AbgBp
AHQAbwByAEMAbwB1AG4AdAAAD00AbwBuAGkAdABvAHIAAA1XAFAAcABhAHQAaAAA
gJ9IAEsARQBZAF8AQwBVAFIAUgBFAE4AVABfAFUAUwBFAFIAXABTAG8AZgB0AHcA
YQByAGUAXABNAGkAYwByAG8AcwBvAGYAdABcAFcAaQBuAGQAbwB3AHMAXABDAHUA
cgByAGUAbgB0AFYAZQByAHMAaQBvAG4AXABFAHgAcABsAG8AcgBlAHIAXABXAGEA
bABsAHAAYQBwAGUAcgBzAAAdQgBhAGMAawBnAHIAbwB1AG4AZABUAHkAcABlAAAA
/kNjnKl8DUCkFvRIhGa4aQAEIAEBCAMgAAEFIAEBEREEIAEBDgQgAQECBSABARFR
JwchDgkOCA4OCQIOAhJFCAICAgICAgICAgICAgIOEhACAgICEkUSRQQAAQEOAyAA
DgUAAgIODgQAAQIOBAABCQ4EIAECDgYAAxwODhwFAAESbQ4HAAICEm0SbQMGEkUF
IAESRQ4EAAAScQUAAg4ODgQAAQ4OCAAEAQ4OHBF5BCABCA4FIAIOCAgFIAIBDhwK
BwYJCQkdDhFJCQYgAR0OHQMIAAQRSQgICAgDIAAFCLd6XFYZNOCJCLA/X38R1Qo6
ARUBCAEbAQcCBggFAAEBHQ4HAAQICAgOCAUgAgEODgQgAQ4OBCABDgkDIAAJBSAB
EQwOBCABAQkEIAEBGAMgABgDIAACCAEACAAAAAAAHgEAAQBUAhZXcmFwTm9uRXhj
ZXB0aW9uVGhyb3dzAQgBAAcBAAAAAAoBAAVXYWxsUAAABQEAAAAAFwEAEkNvcHly
aWdodCDCqSAgMjAyMgAAKQEAJDA1MjRkOWU5LTYxYWUtNDVlMS1hOTg3LTQyOWU5
ZmYwYTEyNAAADAEABzEuMC4wLjAAAE0BABwuTkVURnJhbWV3b3JrLFZlcnNpb249
djQuNi4xAQBUDhRGcmFtZXdvcmtEaXNwbGF5TmFtZRQuTkVUIEZyYW1ld29yayA0
LjYuMSkBACRCOTJCNTZBOS04QjU1LTRFMTQtOUE4OS0wMTk5QkJCNkY5M0IAAAgB
AAEAAAAAACkBACRDMkNGMzExMC00NjBFLTRmYzEtQjlEMC04QTFDMEM5Q0M0QkQA
AAAAAAAAAAA0ytGDAAAAAAIAAABYAAAADDwAAAweAAAAAAAAAAAAAAAAAAAQAAAA
AAAAAAAAAAAAAAAAUlNEU1g6ACQDVDRFndkjfI7lHHkBAAAAQzpcVXNlcnNcTGVz
IEZlcmNoXHNvdXJjZVxyZXBvc1xXYWxsUFxXYWxsUFxvYmpcRGVidWdcV2FsbFAu
cGRiAIw8AAAAAAAAAAAAAKY8AAAAIAAAAAAAAAAAAAAAAAAAAAAAAAAAAACYPAAA
AAAAAAAAAAAAAF9Db3JFeGVNYWluAG1zY29yZWUuZGxsAAAAAAD/JQAgQAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAIA
EAAAACAAAIAYAAAAUAAAgAAAAAAAAAAAAAAAAAAAAQABAAAAOAAAgAAAAAAAAAAA
AAAAAAAAAQAAAAAAgAAAAAAAAAAAAAAAAAAAAAAAAQABAAAAaAAAgAAAAAAAAAAA
AAAAAAAAAQAAAAAAjAMAAJBAAAD8AgAAAAAAAAAAAAD8AjQAAABWAFMAXwBWAEUA
UgBTAEkATwBOAF8ASQBOAEYATwAAAAAAvQTv/gAAAQAAAAEAAAAAAAAAAQAAAAAA
PwAAAAAAAAAEAAAAAQAAAAAAAAAAAAAAAAAAAEQAAAABAFYAYQByAEYAaQBsAGUA
SQBuAGYAbwAAAAAAJAAEAAAAVAByAGEAbgBzAGwAYQB0AGkAbwBuAAAAAAAAALAE
XAIAAAEAUwB0AHIAaQBuAGcARgBpAGwAZQBJAG4AZgBvAAAAOAIAAAEAMAAwADAA
MAAwADQAYgAwAAAAGgABAAEAQwBvAG0AbQBlAG4AdABzAAAAAAAAACIAAQABAEMA
bwBtAHAAYQBuAHkATgBhAG0AZQAAAAAAAAAAADQABgABAEYAaQBsAGUARABlAHMA
YwByAGkAcAB0AGkAbwBuAAAAAABXAGEAbABsAFAAAAAwAAgAAQBGAGkAbABlAFYA
ZQByAHMAaQBvAG4AAAAAADEALgAwAC4AMAAuADAAAAA0AAoAAQBJAG4AdABlAHIA
bgBhAGwATgBhAG0AZQAAAFcAYQBsAGwAUAAuAGUAeABlAAAASAASAAEATABlAGcA
YQBsAEMAbwBwAHkAcgBpAGcAaAB0AAAAQwBvAHAAeQByAGkAZwBoAHQAIACpACAA
IAAyADAAMgAyAAAAKgABAAEATABlAGcAYQBsAFQAcgBhAGQAZQBtAGEAcgBrAHMA
AAAAAAAAAAA8AAoAAQBPAHIAaQBnAGkAbgBhAGwARgBpAGwAZQBuAGEAbQBlAAAA
VwBhAGwAbABQAC4AZQB4AGUAAAAsAAYAAQBQAHIAbwBkAHUAYwB0AE4AYQBtAGUA
AAAAAFcAYQBsAGwAUAAAADQACAABAFAAcgBvAGQAdQBjAHQAVgBlAHIAcwBpAG8A
bgAAADEALgAwAC4AMAAuADAAAAA4AAgAAQBBAHMAcwBlAG0AYgBsAHkAIABWAGUA
cgBzAGkAbwBuAAAAMQAuADAALgAwAC4AMAAAAJxDAADqAQAAAAAAAAAAAADvu788
P3htbCB2ZXJzaW9uPSIxLjAiIGVuY29kaW5nPSJVVEYtOCIgc3RhbmRhbG9uZT0i
eWVzIj8+DQoNCjxhc3NlbWJseSB4bWxucz0idXJuOnNjaGVtYXMtbWljcm9zb2Z0
LWNvbTphc20udjEiIG1hbmlmZXN0VmVyc2lvbj0iMS4wIj4NCiAgPGFzc2VtYmx5
SWRlbnRpdHkgdmVyc2lvbj0iMS4wLjAuMCIgbmFtZT0iTXlBcHBsaWNhdGlvbi5h
cHAiLz4NCiAgPHRydXN0SW5mbyB4bWxucz0idXJuOnNjaGVtYXMtbWljcm9zb2Z0
LWNvbTphc20udjIiPg0KICAgIDxzZWN1cml0eT4NCiAgICAgIDxyZXF1ZXN0ZWRQ
cml2aWxlZ2VzIHhtbG5zPSJ1cm46c2NoZW1hcy1taWNyb3NvZnQtY29tOmFzbS52
MyI+DQogICAgICAgIDxyZXF1ZXN0ZWRFeGVjdXRpb25MZXZlbCBsZXZlbD0iYXNJ
bnZva2VyIiB1aUFjY2Vzcz0iZmFsc2UiLz4NCiAgICAgIDwvcmVxdWVzdGVkUHJp
dmlsZWdlcz4NCiAgICA8L3NlY3VyaXR5Pg0KICA8L3RydXN0SW5mbz4NCjwvYXNz
ZW1ibHk+AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAMAAADAAAALg8AAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAA==
-----END CERTIFICATE-----
-->
