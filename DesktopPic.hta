<!DOCTYPE html>
<!--
DesktopPic (Display picture or slideshow on desktop and/or stamp picture to wallpaper)
Les Ferch, lesferch@gmail.com, GitHub repository created 2022-03-20
DesktopPic.hta (Main program to display and stamp images)
WallP.exe (command line tool to set wallpaper per monitor)
-->
<html>
<head>
<title>DesktopPic</title>
<meta charset="UTF-8" http-equiv="X-UA-Compatible" content="IE=9">
<hta:application
  id=oHTA
  icon="C:\Program Files (x86)\Microsoft\Edge\Application\msedge.exe"
  maximizebutton=no
  minimizebutton=no
  showintaskbar=no
  contextmenu=no
  border=none
  caption=no
  scroll=no
>
<script language="VBScript">

'Begin Main

Window.ResizeTo 1,1
Window.MoveTo -2000,-2000

Const MyVerNum = "1.0.0"
Const WallPVerNum = "1.0.0.0"
Const ForReading = 1
Const ForWriting = 2
Const wiaFormatPNG = "{B96B3CAF-0728-11D3-9D7B-0000F81EF32E}"
Const DevMode = True

Set oWSH = CreateObject("Wscript.Shell")
Set oFSO = CreateObject("Scripting.FileSystemObject")
Set oImg = CreateObject("WIA.ImageFile")
Set oSettings = CreateObject("Scripting.Dictionary")
Set DigitsOnly = New RegExp: DigitsOnly.Pattern = "[^0-9]"
Set DigitsComma = New RegExp: DigitsComma.Pattern = "[^0-9,]"

Dim PicTimer,UpdatePos,w,h,iw,ih,x,y,CenterCoordinates,hTpp,wTpp,Msg,L(6),aImage,MyAppData
ReDim ArrPic(-1)
Z = VBCRLF
Q = Chr(34)

'Copy of HTA for interface, WallP.exe, and processed image, go in MyAppData 
'Note: INI file is stored with app for portability and to allow multiple instances

MyAppData  = oWSH.ExpandEnvironmentStrings("%LocalAppData%") & "\DesktopPic\"
If Not oFSO.FolderExists(MyAppData) Then oFSO.CreateFolder(MyAppData)
WallPExe = MyAppData & "WallP.exe"
NTVer = oWSH.RegRead("HKLM\Software\Microsoft\Windows NT\CurrentVersion\CurrentVersion")

L(0)="Run on startup|Bring to front on start|Stretch smaller pictures|Pic delay in seconds|Max percent of screen area|Centered|Custom|Window position|Stamp|Restore|Continue|Help|Place at least one image file in the folder shown below|No image files found|This option can only be used on the primary monitor.|Press ⊞-P to temporarily change the primary monitor.|Wallpaper style must be set to Fill to use this option|Change wallpaper style to Fill?|Wallpaper file missing:|Background type must be set to Picture to use this option|Change background type to picture?"
L(1)="Beim Start ausführen|Beim Start nach vorne bringen|Strecken Sie kleinere Bilder|Bildverzögerung in Sekunden|Maximaler Prozentsatz der Bildschirmfläche|Zentriert|Brauch|Fensterposition|Briefmarke|Wiederherstellen|Fortsetzen|Hilfe|Legen Sie mindestens eine Bilddatei in dem unten angezeigten Ordner ab|Keine Bilddateien gefunden|Diese Option kann nur auf dem primären Monitor verwendet werden|Drücken Sie ⊞-P, um den primären Monitor vorübergehend zu ändern.|Hintergrundstil muss auf Ausfüllen eingestellt sein, um diese Option zu verwenden|Hintergrundstil in Ausfüllen ändern?|Wallpaper-Datei fehlt:"
L(2)="Corre al empezar|Traer al frente al inicio|Estirar imágenes más pequeñas|Retardo de imagen en segundos|Porcentaje máximo del área de la pantalla|Centrado|Personalizado|Posición de la ventana|Sello|Restaurar|Continuar|Ayuda|Coloque al menos un archivo de imagen en la carpeta que se muestra a continuación|No se encontraron archivos de imagen|Esta opción solo se puede usar en el monitor principal|Presione ⊞-P para cambiar temporalmente el monitor principal.|El estilo de fondo de pantalla debe establecerse en Rellenar para usar esta opción|¿Cambiar el estilo de fondo de pantalla a Rellenar?|Falta el archivo del fondo de pantalla:"
L(3)="Exécuter au démarrage|Amenez à l'avant au démarrage|Étirez des images plus petites|Délai d'image en secondes|Pourcentage maximum de la zone d'écran|Centré|Personnalisé|Position de la fenêtre|Timbre|Restaurer|Continuez|Aide|Placez au moins un fichier image dans le dossier indiqué ci-dessous|Aucun fichier image trouvé|Cette option ne peut être utilisée que sur le moniteur principal|Appuyez sur ⊞-P pour changer temporairement le moniteur principal.|Le style de papier peint doit être défini sur Remplir pour utiliser cette option|Changer le style de fond d'écran en Remplir ?|Fichier de fond d'écran manquant :"
L(4)="起動時に実行する|スタート時に前面に出す|小さい画像を伸ばす|秒単位の画像の遅延|画面領域の最大パーセント|中央揃え|カスタム|ウィンドウの位置|切手|戻す|継続する|ヘルプ|以下に示すフォルダに少なくとも1つの画像ファイルを配置します|画像ファイルが見つかりません|このオプションは、プライマリモニターでのみ使用できます|⊞-Pを押して、プライマリモニターを一時的に変更します。|このオプションを使用するには、壁紙のスタイルを塗りつぶしに設定する必要があります|壁紙のスタイルを塗りつぶしに変更しますか？|壁紙ファイルがありません:"
L(5)="在启动时运行|开始时放在前面|拉伸较小的图片|图片延迟以秒为单位|最大屏幕面积百分比|居中|风俗|窗口位置|邮票|恢复|继续|帮助|将至少一个图像文件放入如下所示的文件夹中|未找到图像文件|此选项只能在主显示器上使用|按 ⊞-P 临时更改主监视器。|墙纸样式必须设置为填充才能使用此选项|将壁纸样式更改为填充？|壁纸文件丢失："
L(6)="在啟動時運行|開始時放在前面|拉伸較小的圖片|圖片延遲以秒為單位|最大屏幕面積百分比|居中|風俗|窗口位置|郵票|恢復|繼續|幫助|在如下所示的文件夾中至少放置一個圖像文件|未找到圖像文件|此選項只能在主顯示器上使用|按 ⊞-P 臨時更改主監視器。|牆紙樣式必須設置為填充才能使用此選項|將壁紙樣式更改為填充？|壁紙文件丟失："

RunOnStartup = False
BringToFront = True
Stretch = False
PicDelay = 5
MaxScrPct = 25
Coordinates = ""
Forward = True
DoingInput = False
PicIndex = -1
LangIdx=0
On Error Resume Next
Language = oWSH.RegRead("HKCU\Control Panel\International\LocaleName")
Language = oWSH.RegRead("HKCU\Control Panel\Desktop\PreferredUILanguages")(0)
If Left(Language,2)="de" Then LangIdx=1
If Left(Language,2)="es" Then LangIdx=2
If Left(Language,2)="fr" Then LangIdx=3
If Left(Language,2)="ja" Then LangIdx=4
If Language="zh-CN" Then LangIdx=5
If Language="zh-TW" Then LangIdx=6
On Error Goto 0

OptionMode = oHTA.caption="yes"
BorderSize = 0: If OptionMode Then BorderSize = 8
CaptionHeight = 30

CmdLine = Replace(oHTA.commandLine,Q & "  " & Q,"|")
CmdLine = Replace(CmdLine,Q & " " & Q,"|")
CmdLine = Replace(CmdLine,Q,"")
aParam = Split(CmdLine,"|")

MyPath  = Mid(document.URL,8)
MyFolder = oFSO.GetParentFolderName(MyPath)
MyName = oFSO.GetFileName(MyPath)
OptionModeHTA = MyAppData & MyName

If UBound(aParam)>0 Then
  If IsNumeric(aParam(1)) Then PicIndex = aParam(1) - 1
End If

If OptionMode Then
  PicPath = aParam(2) & "\"
  HTAName = aParam(3)
  MyTitle = Split(HTAName,".")(0)
  oWSH.CurrentDirectory = PicPath
Else
  MyTitle = Split(MyName,".")(0)
  oWSH.CurrentDirectory = MyFolder
  PicPath = oWSH.CurrentDirectory & "\"
End If

INIFile = PicPath & MyTitle & ".ini"

If Not(Instr(MyFolder,"C:\Program Files\")>0 Or Instr(MyFolder,"C:\Program Files (x86)\")>0) Then
  TestFile = ".\" & Replace(Replace(Replace(Now(),"/",""),":","")," ","") & ".txt"
  On Error Resume Next
  Set oFile = oFSO.OpenTextFile(TestFile,ForWriting,True,Ansi)
  oFile.Write ""
  oFile.Close
  On Error Goto 0
  If oFSO.FileExists(TestFile) Then
    oFSO.DeleteFile(TestFile)
  Else
    INIFile = MyAppData & MyTitle & ".ini"
  End If
End If

If NTVer>"6.1" Then
  If oFSO.FileExists(WallPExe) Then
    Set oFile = oFSO.OpenTextFile(WallPExe,ForReading)
    FileContents = oFile.ReadAll
    oFile.Close
    If Instr(FileContents,WallPVerNum)=0 Then oFSO.DeleteFile(WallPExe)
  End If  

  If Not oFSO.FileExists(WallPExe) Then
    oWSH.Run "certutil -decode " & Q & MyPath & Q & " " & Q & WallPExe & Q,0,False
  End If
End If

Ini2Dict

If oSettings.Exists("[Options]LangIdx") Then LangIdx = oSettings.Item("[Options]LangIdx")
If oSettings.Exists("[Options]RunOnStartup") Then RunOnStartup = (LCase(oSettings.Item("[Options]RunOnStartup"))="true")
If oSettings.Exists("[Options]BringToFront") Then BringToFront = (LCase(oSettings.Item("[Options]BringToFront"))="true")
If oSettings.Exists("[Options]Stretch") Then Stretch = (LCase(oSettings.Item("[Options]Stretch"))="true")
If oSettings.Exists("[Options]PicDelay") Then PicDelay = oSettings.Item("[Options]PicDelay")
If oSettings.Exists("[Options]MaxScrPct") Then MaxScrPct = oSettings.Item("[Options]MaxScrPct")
If oSettings.Exists("[Options]Coordinates") Then Coordinates = oSettings.Item("[Options]Coordinates")
If Stretch="" Then Stretch = False
If PicDelay="" Then PicDelay = 5
If MaxScrPct="" Then MaxScrPct = 25

Msg = Split(L(LangIdx),"|")

Set oFolder = oFSO.GetFolder(PicPath)
Set oFiles = oFolder.Files
For Each oFile In oFiles
  On Error Resume Next
  oImg.LoadFile oFile.Name
  If Err.Number=0 Then IsImage=True Else IsImage=False
  On Error Goto 0
  If IsImage Then
    iw = oImg.Width: ih = oImg.Height
    ReDim Preserve ArrPic(UBound(ArrPic)+1)
    ArrPic(UBound(ArrPic)) = oFile.Name & "|" & iw & "|" & ih
  End If
Next

If UBound(ArrPic)=-1 Then
  MsgBox Msg(12) & Z & Z & MyFolder,vbOKonly,Msg(13)
  self.close
End If

'End Main


Sub Ini2Dict
  If oFSO.FileExists(INIFile) Then 
    Set oFile = oFSO.OpenTextFile(INIFile,ForReading)
    Do Until oFile.AtEndOfStream
      Line = Trim(oFile.ReadLine)
      If Line<>"" And Left(Line,1)<>";" Then
        If Left(Line,1)="[" Then
          Section = Line
        Else
          ArrLine = Split(Line,"=")
          If UBound(Arrline)=1 Then oSettings.Add Section & ArrLine(0),ArrLine(1)
        End If
      End If
    Loop
    oFile.Close
  End If
End Sub

Sub window_onLoad

  If OptionMode Then
    document.body.style.backgroundPosition="0px -" & CaptionHeight & "px"
    ShowOptions
    If LangIdx<>0 Then UpdateLang
  Else
    HideOptions
  End If
  
  If NTVer<"6.2" Then
    Bsw.Disabled = True
    Brw.Disabled = True
  End If
  
  Document.Title = MyTitle
  If Not BringToFront Then Window.blur

  LoadPic

  If Coordinates<>"" Then RepositionWindow

  ToggleShortcut

  wTpp = Window.outerWidth: hTpp = Window.outerHeight

  If OptionMode Then
    UpdatePos = Window.SetInterval("UpdatePosition()", 100)
  Else
    If UBound(ArrPic)>0 Then PicTimer = Window.SetInterval("LoadPic()", PicDelay*1000)
  End If

End Sub

Sub UpdateLang
  Msg = Split(L(LangIdx),"|")
  M0.innerHTML = Msg(0)
  M1.innerHTML = Msg(1)
  M2.innerHTML = Msg(2)
  M3.innerHTML = Msg(3)
  M4.innerHTML = Msg(4)
  M5.innerHTML = Msg(5)
  M6.innerHTML = Msg(6)
  M7.innerHTML = Msg(7)
  Bsw.Value = Msg(8)
  Brw.Value = Msg(9)
  Bcn.Value = Msg(10)
  Bhp.Value = Msg(11)
End Sub

Sub UpdateXY
  aCoords = Split(Coordinates,",")
  x = CInt(aCoords(0))
  y = CInt(aCoords(1))
End Sub

Sub RepositionWindow
  UpdateXY
  Window.MoveTo x - BorderSize, y
End Sub

Sub ToggleShortcut
  Crs.blur
  RunOnstartup = Crs.Checked
  ShortCutFolder = oWSH.SpecialFolders("Startup")
  ShortcutExists = False
  Set oFolder = oFSO.GetFolder(ShortCutFolder)
  Set oFiles = oFolder.Files
  For Each oFile In oFiles
    If LCase(oFSO.GetExtensionName(oFile.Name)) = "lnk" Then
      Set link = oWSH.CreateShortcut(ShortCutFolder & "\" & oFile.Name)
      If link.TargetPath=PicPath & HTAName Then
        ShortcutExists = True
        If Not RunOnstartup Then oFSO.DeleteFile ShortCutFolder & "\" & oFile.Name
      End If
    End If
  Next
  If RunOnstartup And Not ShortcutExists Then
    ShortCutPath = ShortCutFolder & "\" & MyTitle & Replace(Replace(Replace(Now(),"/",""),":","")," ","") & ".lnk"
    Set link = oWSH.CreateShortcut(ShortCutPath)
    link.TargetPath = PicPath & HTAName
    link.Save
  End If
End Sub

Sub UpdatePosition
  sw = screen.availWidth
  sh = screen.availHeight
  w = Window.outerWidth
  h = Window.outerHeight
  UpdateTpp = Not(w=wTpp And h=hTpp)
  w = w - (BorderSize * 2)
  h = h - BorderSize
  If UpdateTpp Then
    NewSize = Cint((w * h) / (sw * sh) * 100)
    If NewSize>100 Then NewSize = 100
    If NewSize<=0 Then NewSize = 1
    Tpp.Value = NewSize
    MaxScrPct = Tpp.Value
  Else
    Tpp.Value = MaxScrPct
  End If
  Tsp.Value = window.screenX + BorderSize & "," & window.screenY
  Coordinates = Tsp.Value
  UpdateXY
  If Mpp.SelectedIndex=0 And Tsp.Value<>CenterCoordinates Then
    Mpp.SelectedIndex=1
    Tsp.style.visibility = "visible"
  End If
End Sub

Sub LoadPic
  If Forward Then PicIndex = PicIndex + 1 Else PicIndex = PicIndex - 1
  If PicIndex=UBound(ArrPic)+1 Then PicIndex = 0
  If PicIndex<0 Then PicIndex = UBound(ArrPic)
  aImage = Split(ArrPic(PicIndex),"|")
  iw = aImage(1): ih = aImage(2)
  ResizePic
  window.document.body.background = PicPath & aImage(0)
  ResizeWindow
  If Coordinates="" Then CenterWindow
End Sub

Sub ResizePic
  sw = screen.availWidth
  sh = screen.availHeight
  MaxArea = sw * sh * (MaxScrPct/100)
  ImgArea = iw * ih
  w = iw: h = ih
  If ImgArea>MaxArea Or Stretch Or OptionMode Then
    ResizeFactor = Sqr(MaxArea/ImgArea)
    w = iw * ResizeFactor
    h = ih * ResizeFactor
    If w>sw Then h = sw/w * h : w = sw
    If h>sh Then w = sh/h * w : h = sh
    w = CInt(w): h = CInt(h)
    h = h + BorderSize: w = w + (BorderSize * 2)
  End If
End Sub

Sub ResizeWindow
	Window.ResizeTo w, h
End Sub

Sub CenterWindow
  sw = screen.availWidth
  sh = screen.availHeight
	Window.MoveTo (sw - w)/2, (sh - h)/2
  CenterCoordinates = window.screenX + BorderSize & "," & window.screenY
End Sub

Sub ShowOptions
  document.body.style.visibility = "visible"
  Crs.Style.Zoom = 1.2
  Cbf.Style.Zoom = 1.2
  Csp.Style.Zoom = 1.2
  Crs.Checked = RunOnStartup
  Cbf.Checked = BringToFront
  Csp.Checked = Stretch
  Tpd.Value = PicDelay
  Tpp.Value = MaxScrPct
  Tsp.Value = Coordinates
  If Coordinates="" Then
    Tsp.style.visibility = "hidden"
  Else
    mpp.SelectedIndex = 1
    Tsp.style.visibility = "visible"
  End If
  Mln.SelectedIndex = LangIdx
End Sub

Sub HideOptions
  document.body.style.visibility = "hidden"
  Tsp.style.visibility = "hidden"
End Sub

Sub document_onclick
  If OptionMode And Not DoingInput Then
    ResizePic
    ResizeWindow
    Var2Ini
  End If
End Sub

Sub ManualAdvance(Flag)
  If Not DoingInput Then
    window.event.keyCode = 0
    clearInterval(PicTimer)
    Forward = Flag
    If PicTimer=0 Then LoadPic
    PicTimer = 0
  End If
End Sub

Sub document_onKeyDown
  Select Case window.event.keyCode
    Case 32, 39 'Spacebar, Right arrow
      ManualAdvance(True)
    Case 8, 37 'Backspace, Left arrow
      ManualAdvance(False)
    Case 13 'Enter
      ContinueShow
    Case 27 'Esc
      If Not OptionMode Then LaunchOptionModeHTA
    Case 116 'F5
      window.event.keyCode = 0
  End Select
End Sub

Sub ContinueShow
  If OptionMode Then
    oWSH.Run Q & PicPath & HTAName & Q & " " & Q & PicIndex & Q,1,False
    self.close
  Else
    HideOptions
    Forward = True
    clearInterval(PicTimer)
    PicTimer = Window.SetInterval("LoadPic()", PicDelay*1000)
  End If
End Sub

Sub Help
  oWSH.Run "https://lesferch.github.io/DesktopPic"
End Sub

Sub LaunchOptionModeHTA
  clearInterval(PicTimer)
  If oFSO.FileExists(OptionModeHTA) Then
    If DevMode Then
      oFSO.DeleteFile(OptionModeHTA)
    Else
      Set oFile = oFSO.OpenTextFile(OptionModeHTA,ForReading)
      FileContents = oFile.ReadAll
      oFile.Close
      If Instr(FileContents,Q & MyVerNum & Q)=0 Then oFSO.DeleteFile(OptionModeHTA)
    End If
  End If  
  If Not oFSO.FileExists(OptionModeHTA) Then
    Set oFile = oFSO.OpenTextFile(MyPath,ForReading)
    MyCode = oFile.ReadAll
    oFile.Close
    MyCode = Replace(MyCode,LCase("CAPTION=NO"),"caption=yes")
    MyCode = Replace(MyCode,LCase("BORDER=NONE"),"border=thick")
    MyCode = Replace(MyCode,LCase("SCROLL=NO"),"scroll=auto")
    Set oFile = oFSO.OpenTextFile(OptionModeHTA,ForWriting,True)
    oFile.Write MyCode
    oFile.Close
  End If
  oWSH.Run Q & OptionModeHTA & Q & " " & Q & PicIndex & Q & " " & Q & MyFolder & Q & " " & Q & MyName & Q,1,False
  self.close
End Sub

Sub OnFocusInput
  DoingInput = True
  ClearInterval(UpdatePos)
End Sub

Sub OnBlurInput
  DoingInput = False
  UpdatePos = Window.SetInterval("UpdatePosition()", 500)
End Sub

Sub OnInputDigits(id)
  document.getElementByID(id).Value = DigitsOnly.Replace(document.getElementByID(id).Value,"")
End Sub

Sub OnInputDigitsComma(id)
  document.getElementByID(id).Value = DigitsComma.Replace(document.getElementByID(id).Value,"")
End Sub

Sub OnChangeTpd
  If Tpd.Value<1 Then Tpd.Value = 1
End Sub

Sub OnChangeTpp
  If Tpp.Value<1 Or Tpp.Value>100 Then
    Tpp.Value = MaxScrPct
  Else
    MaxScrPct = Tpp.Value
  End If
  ResizePic
  ResizeWindow
  wTpp = Window.outerWidth: hTpp = Window.outerHeight
End Sub

Sub OnChangeMPP
  Mpp.blur
  If Mpp.SelectedIndex=1 Then
    Tsp.style.visibility = "visible"
  Else
    CenterWindow
    Tsp.style.visibility = "hidden"
  End If
End Sub

Sub OnChangeMln
  LangIdx = Mln.SelectedIndex
  UpdateLang
End Sub

Sub OnChangeTsp
  If tsp.Value<>"" Then 
    Coordinates = Tsp.Value
    If Left(Coordinates,1)="," Then Coordinates = "0" & Coordinates
    If Instr(Coordinates,",")=0 Then Coordinates = Coordinates & ",0"
    RepositionWindow
  End If
End Sub

Sub window_onUnload
  If OptionMode Then Var2Ini
End Sub

Sub Var2Ini
  Set oFile = oFSO.OpenTextFile(INIFile,ForWriting,True)
  oFile.Write "[Options]" & Z
  oFile.Write "LangIdx=" & LangIdx & Z
  oFile.Write "RunOnStartup=" & Crs.Checked & Z
  oFile.Write "BringToFront=" & Cbf.Checked & Z
  oFile.Write "Stretch=" & Csp.Checked & Z
  oFile.Write "PicDelay=" & Tpd.Value & Z
  oFile.Write "MaxScrPct=" & Tpp.Value & Z
  If Mpp.SelectedIndex=0 Then Tsp.Value=""
  oFile.Write "Coordinates=" & Tsp.Value & Z
  oFile.Close
End Sub

Sub CheckPrimary
End Sub

Sub RestoreWallpaper
  Brw.blur

  If (x<0 Or y<0 Or x>screen.Width Or y>screen.Height) Then
    MsgBox Msg(14) & Z & Z & Msg(15),vbOKonly,Msg(9)
    Exit Sub
  End If

  oWSH.Run Q & WallPExe & Q & " 0",0,True
  Monitor = oWSH.RegRead("HKCU\Software\WallP\Monitor")
  WPpath = oWSH.RegRead("HKCU\Software\WallP\WPpath")

  On Error Resume Next
  WPbackup = oWSH.RegRead("HKCU\Software\WallP\" & Monitor & "\WPbackup")
  On Error Goto 0
  If oFSO.FileExists(WPbackup) Then
    oWSH.Run Q & WallPExe & Q & " 0 " & Q & WPbackup & Q,0,True
  End If

  StampedFile = MyAppData & "DesktopPic" & Monitor & ".png"
  If oFSO.FileExists(WPpath) Then
    If LCase(WPpath)<>LCase(StampedFile) Then oWSH.RegWrite "HKCU\Software\WallP\" & Monitor & "\WPbackup",WPpath
  End If

End Sub

Sub StampWallpaper
  Bsw.blur

  If (x<0 Or y<0 Or x>screen.Width Or y>screen.Height) Then
    MsgBox Msg(14) & Z & Z & Msg(15),vbOKonly,Msg(8)
    Exit Sub
  End If

  'Chek for Fill style
  WallpaperStyle = oWSH.RegRead("HKCU\Control Panel\Desktop\WallpaperStyle")
  If WallpaperStyle<>"10" Then
    Answer = MsgBox(Msg(16) & Z & Z & Msg(17),vbYesNo,Msg(8))
    If Answer = vbNo Then Exit Sub
  End If

  'Detect if slideshow is enabled
  BackgroundType = 0
  On Error Resume Next
  BackgroundType = oWSH.RegRead("HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\Wallpapers\BackgroundType")
  On Error Goto 0
  If BackgroundType=2 Then
    Answer = MsgBox(Msg(19) & Z & Z & Msg(20),vbYesNo,Msg(8))
    If Answer = vbNo Then Exit Sub
  End If
  
  sw = screen.availWidth
  sh = screen.availHeight
  Fsw = screen.Width
  Fsh = screen.Height

  StampImg = PicPath & aImage(0)
  oWSH.Run Q & WallPExe & Q & " 0",0,True
  Monitor = oWSH.RegRead("HKCU\Software\WallP\Monitor")
  WPpath = oWSH.RegRead("HKCU\Software\WallP\WPpath")
  StampedFile = MyAppData & "DesktopPic" & Monitor & ".png"

  'Solid color background
  If BackgroundType=1 Then
    Colors = Split(oWSH.RegRead("HKCU\Control Panel\Colors\Background")," ")
    c = CLng("&hFF" & Right("0" & Hex(Colors(0)),2) & Right("0" & Hex(Colors(1)),2) & Right("0" & Hex(Colors(2)),2))

    Pad = Int(0.1 * Math.Max(Fsw,Fsh))

    Set v = CreateObject("WIA.Vector")
    For i = 1 To 400
      v.Add c
    Next
    Set Img = v.ImageFile(20,20)
    Set IP = CreateObject("WIA.ImageProcess")
    IP.Filters.Add IP.FilterInfos("Scale").FilterID
    IP.Filters(1).Properties("MaximumWidth") = Fsw + Pad
    IP.Filters(1).Properties("MaximumHeight") = Fsh + Pad
    IP.Filters(1).Properties("PreserveAspectRatio") = KeepAspect
    IP.Filters.Add IP.FilterInfos("Crop").FilterID
    IP.Filters(2).Properties("Right") = Pad
    IP.Filters(2).Properties("Bottom") = Pad
    IP.Filters.Add IP.FilterInfos("Convert").FilterID
    IP.Filters(3).Properties("FormatID").Value = wiaFormatPNG
    Set Img = IP.Apply(Img)
    
    SolidColorImage = MyAppData & "SolidColorImage.png"

    If oFSO.FileExists(SolidColorImage) Then oFSO.DeleteFile SolidColorImage
    Img.SaveFile SolidColorImage
    Set Img=Nothing
    Set IP=Nothing

    WPpath = SolidColorImage

    oWSH.Run Q & WallPExe & Q & " 0 Fill " & Q & SolidColorImage & Q,0,True
    MonitorCount = oWSH.RegRead("HKCU\Software\WallP\MonitorCount")
    If MonitorCount>1 Then
      For i = 1 To MonitorCount-1
        oWSH.Run Q & WallPExe & Q & " " & i & " Fill " & Q & SolidColorImage & Q,0,True
      Next
    End If

  End If
   
  If oFSO.FileExists(WPpath) Then
    If LCase(WPpath)<>LCase(StampedFile) Then oWSH.RegWrite "HKCU\Software\WallP\" & Monitor & "\WPbackup",WPpath
  Else
    MsgBox Msg(18) & Z & Z & WPpath
    Exit Sub
  End If

  'Scale image to be stamped
  Set Img = CreateObject("WIA.ImageFile")
  Img.LoadFile StampImg
  Set IP = CreateObject("WIA.ImageProcess")
  IP.Filters.Add IP.FilterInfos("Scale").FilterID
  IP.Filters(1).Properties("MaximumWidth") = w
  IP.Filters(1).Properties("MaximumHeight") = h
  Set Img = IP.Apply(Img)
  Set IP=Nothing

  'Load current wallpaper
  Set Wall = CreateObject("WIA.ImageFile")
  Wall.LoadFile WPpath

  'Adjust current wallpaper to fill screen
  If Not(Wall.Width=Fsw And Wall.Height=Fsh) Then
  
    wFactor = sw / Wall.Width
    hFactor = sh / Wall.Height
    If wFactor>hFactor Then NewWidth = sw : NewHeight = Int(wFactor * Wall.Height)
    If hFactor>wFactor Then NewHeight = sh : NewWidth = Int(hFactor * Wall.Width)
    Set IP = CreateObject("WIA.ImageProcess")
    IP.Filters.Add IP.FilterInfos("Scale").FilterID
    IP.Filters(1).Properties("MaximumWidth") = NewWidth
    IP.Filters(1).Properties("MaximumHeight") = NewHeight
    IP.Filters(1).Properties("PreserveAspectRatio") = KeepAspect
    Set Wall = IP.Apply(Wall)
    Set IP=Nothing
    NewWidth = Wall.Width
    NewHeight = Wall.Height

    'Crop excess width or height
    If NewWidth>Fsw Or NewHeight>Fsh Then
      LeftCrop = 0
      RightCrop = 0
      TopCrop = 0
      BotCrop = 0
      If NewWidth>Fsw Then
        LeftCrop = Int((NewWidth - Fsw) / 2)
        RightCrop = NewWidth - Fsw - LeftCrop
      End If
      If NewHeight>Fsh Then
        TopCrop = Int((NewHeight - Fsh) / 2)
        TopCrop = Int(TopCrop * 2 / 3)
        BotCrop = NewHeight - Fsh - TopCrop
      End If

      Set IP = CreateObject("WIA.ImageProcess")
      IP.Filters.Add IP.FilterInfos("Crop").FilterID
      IP.Filters(1).Properties("Left") = LeftCrop
      IP.Filters(1).Properties("Right") = RightCrop
      IP.Filters(1).Properties("Top") = TopCrop
      IP.Filters(1).Properties("Bottom") = BotCrop
      Set Wall = IP.Apply(Wall)
      Set IP=Nothing

    End If
    
  End If

  'Stamp image
  Set IP = CreateObject("WIA.ImageProcess")
  IP.Filters.Add IP.FilterInfos("Stamp").FilterID
  Set IP.Filters(1).Properties("ImageFile") = Img
  IP.Filters(1).Properties("Left") = window.screenX + BorderSize
  IP.Filters(1).Properties("Top") = window.screenY
  IP.Filters.Add IP.FilterInfos("Convert").FilterID
  IP.Filters(2).Properties("FormatID").Value = wiaFormatPNG
  Set Wall = IP.Apply(Wall)
  If oFSO.FileExists(StampedFile) Then oFSO.DeleteFile StampedFile
  Wall.SaveFile StampedFile
  Set Wall=Nothing
  Set Img=Nothing
  Set IP=Nothing

  oWSH.Run Q & WallPExe & Q & " 0 Fill " & Q & StampedFile & Q,0,True
  
End Sub

</script>
<style>
body {background-size:cover; background-attachment:fixed; background-repeat:no-repeat}
br {font-size: 16pt} 
.cc {
font-family: sans-serif;
font-weight: normal;
font-size: 11pt;
color: white;
background: rgba(0,0,0,.4);
padding: 1pt;
border-radius: 2pt;
}
</style>
</head>
<body>
<input type=button id=Bsw Value=Stamp onclick=StampWallpaper()>
<input type=button id=Brw Value=Restore onclick=RestoreWallpaper()>
<input type=button id=Bcn Value=Continue onclick=ContinueShow()>
<input type=button id=Bhp Value=Help onclick=Help()>
<br>
<select id=Mln  OnChange=OnChangeMln()>
<option>English</option>
<option>Deutsch</option>
<option>Español</option>
<option>Français</option>
<option>日本語</option>
<option>中文 (简体)</option>
<option>中文 (繁體)</option>
</select><br>
<input type=checkbox id=Crs OnClick=ToggleShortcut()>
<span id=M0 class=cc>Run on startup</span><br>
<input type=checkbox id=Cbf OnClick=Cbf.blur()>
<span id=M1 class=cc>Bring to front on start</span><br>
<input type=checkbox id=Csp OnClick=Csp.blur()>
<span id=M2 class=cc>Stretch smaller pictures</span><br>
<input type=text size=5 id=Tpd MaxLength=5 OnFocus=OnFocusInput() OnBlur=OnBlurInput() OnInput=OnInputDigits("Tpd") OnChange=OnChangeTpd()>
<span id=M3 class=cc>Pic delay in seconds</span><br>
<input type=text size=5 id=Tpp MaxLength=3 OnFocus=OnFocusInput() OnBlur=OnBlurInput() OnInput=OnInputDigits("Tpp") OnChange=OnChangeTpp()>
<span id=M4 class=cc>Max percent of screen area</span><br>

<select id=Mpp  OnChange=OnChangeMpp()>
<option id=M5>Centered</option>
<option id=M6>Custom</option>
</select>
<span id=M7 class=cc>Window position</span>
<input type=text size=11 id=Tsp MaxLength=11 OnFocus=OnFocusInput() OnBlur=OnBlurInput() OnInput=OnInputDigitsComma("Tsp") OnChange=OnChangeTsp()>
</body>
</html>
<!--
This is a base64 encoding of WallP.exe
-----BEGIN CERTIFICATE-----
TVqQAAMAAAAEAAAA//8AALgAAAAAAAAAQAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAgAAAAA4fug4AtAnNIbgBTM0hVGhpcyBwcm9ncmFtIGNhbm5v
dCBiZSBydW4gaW4gRE9TIG1vZGUuDQ0KJAAAAAAAAABQRQAATAEDAA0/RLoAAAAA
AAAAAOAAIgALATAAABoAAAAIAAAAAAAATjgAAAAgAAAAQAAAAABAAAAgAAAAAgAA
BAAAAAAAAAAGAAAAAAAAAACAAAAAAgAAAAAAAAMAYIUAABAAABAAAAAAEAAAEAAA
AAAAABAAAAAAAAAAAAAAAPw3AABPAAAAAEAAAIwFAAAAAAAAAAAAAAAAAAAAAAAA
AGAAAAwAAABsNwAAOAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAIAAACAAAAAAAAAAAAAAACCAAAEgAAAAAAAAAAAAAAC50ZXh0AAAA
VBgAAAAgAAAAGgAAAAIAAAAAAAAAAAAAAAAAACAAAGAucnNyYwAAAIwFAAAAQAAA
AAYAAAAcAAAAAAAAAAAAAAAAAABAAABALnJlbG9jAAAMAAAAAGAAAAACAAAAIgAA
AAAAAAAAAAAAAAAAQAAAQgAAAAAAAAAAAAAAAAAAAAAwOAAAAAAAAEgAAAACAAUA
3CQAAJASAAADAAIAAQAABgAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAABswBABtAwAAAQAAEQByAQAAcAog/8maOwsfYw1yAQAA
cBMEAo4W/gETBhEGOYAAAAAAcgMAAHAoEAAACgByUQAAcCgQAAAKAHLsAABwKBAA
AAoAcj4BAHAoEAAACgByiAEAcCgQAAAKAHLsAQBwKBAAAAoAcnMCAHAoEAAACgBy
CAMAcCgQAAAKAHJ8AwBwKBAAAAoAchEEAHAoEAAACgByewQAcCgQAAAKAAA4xwIA
AAAWEwg4/AAAAAACEQiabxEAAApyCgUAcCgSAAAKEwkRCSwEABYNAAIRCJpvEQAA
CnIYBQBwKBIAAAoTChEKLAQAFw0AAhEImm8RAAAKciIFAHAoEgAAChMLEQssBAAY
DQACEQiabxEAAApyMgUAcCgSAAAKEwwRDCwEABkNAAIRCJpvEQAACnI6BQBwKBIA
AAoTDRENLAQAGg0AAhEImm8RAAAKckQFAHAoEgAAChMOEQ4sBAAbDQACEQiaKBMA
AAoTDxEPLAcAAhEImgoAAAACEQiaKBQAAAoLAN4FJgAA3gACEQiack4FAHBvFQAA
ChMQERAsCAACEQiaEwQAABEIF1gTCBEIAo5p/gQTEREROvT+//9zEAAABnQEAAAC
EwcAEQcHbwYAAAYMAN4LJgByAQAAcAwA3gAGcgEAAHAoFgAAChMSERIsOwAoFwAA
Cm8YAAAKBigZAAAKKBoAAAoKEQcIBm8EAAAGAAkfY/4BFv4BExMREywLABEHCW8L
AAAGAAAAEQcIbwUAAAYKAAgIclIFAHBvGwAACh8LbxwAAAoTBQDeDCYAcloFAHAT
BQDeAAcg/8maO/4BBxEHbwcAAAb+BWATFBEUOdQAAAAAfh0AAApyYgUAcG8eAAAK
ExURFXJ0BQBwbx4AAAoTFgARFnKABQBwEQdvBwAABowdAAABbx8AAAoAERZymgUA
cBEFbx8AAAoAERZyqgUAcAZvHwAACgARFhEFbx4AAAoTFwARF3KqBQBwBm8fAAAK
AADeDREXLAgRF28gAAAKANwRFXK4BQBwbx4AAAoTGAARGHIiBgBwFowfAAABGm8h
AAAKAADeDREYLAgRGG8gAAAKANwA3g0RFiwIERZvIAAACgDcEQUoEAAACgAGKBAA
AAoAABEEcgEAAHAoFgAAChMZERksEQARBxEEKAIAAAZvCQAABgAAACoAAAABTAAA
AAB0AQ6CAQUQAAABAADGAQ3TAQsQAAABAAAzAhlMAgwQAAABAgDWAhLoAg0AAAAA
AgADAxgbAw0AAAAAAgCUApcrAw0AAAAAGzAFAIIAAAACAAARABYKFgsWDAIXjSEA
AAElFh8snW8iAAAKDQAJFpooFAAACgoA3gUmAADeAAAACReaKBQAAAoLAN4FJgAA
3gAAAAkYmigUAAAKDADeBSYAAN4AFgbSB9II0igjAAAKEwQSBCgkAAAKEgQoJQAA
Ch5iYBIEKCYAAAofEGJgEwUrABEFKgAAASgAAAAAGQANJgAFEAAAAQAALAANOQAF
EAAAAQAAPwANTAAFEAAAASICKCcAAAoAKgAAAEJTSkIBAAEAAAAAAAwAAAB2NC4w
LjMwMzE5AAAAAAUAbAAAAIwEAAAjfgAA+AQAAAgFAAAjU3RyaW5ncwAAAAAACgAA
QAYAACNVUwBAEAAAEAAAACNHVUlEAAAAUBAAAEACAAAjQmxvYgAAAAAAAAACAAAB
VzUCAAkCAAAA+gEzABYAAAEAAAAhAAAABQAAAAQAAAAQAAAAEAAAACcAAAARAAAA
DAAAAAIAAAABAAAAAgAAAAMAAAAAAGMCAQAAAAAABgDPAdoDBgA8AtoDBgDsAKgD
DwD6AwAABgAUAfECBgCyAfECBgCTAfECBgAjAvECBgDvAfECBgAIAvECBgBCAfEC
BgAAAbsDBgDeALsDBgB2AfECBgBdAXUCBgBWBMICBgDEBAEACgCWA6ACBgDMAMIC
BgC7ALsDBgArAbsDBgChAMICBgCPAsICBgCcAEAABgCIBMICBgDVAsICBgC2AkAA
BgDiBAEABgATAMICBgCQAMICBgAUAMICBgB3AAEABgAoA8ICAAAAABoAAAAAAAEA
AQAAABAASgA5AEEAAQABAAoBEABRBAAATQABAAQAohAAAC0DAAAAAAUABAACEBAA
HQQAAEEABQAQAAYAXQTNAAYAJAPNAAYAYgTNAAYAyQLNAFAgAAAAAJEA0ALQAAEA
GCQAAAAAkQCTA1YAAgDQJAAAAACGGKIDBgADAAAAAAAAAMYFTAPWAAMAAAAAAAAA
xgU/A9wABQAAAAAAAADGBToE4QAHAAAAAAAAAMYFbgTmAAkAAAAAAAAAxgVWAOoA
CgAAAAAAAADGBYAD8AAMAAAAAAAAAMYFbQPmAA0AAAAAAAAAxgUPAwEADgAAAAAA
AADGBQMDRwAPAAAAAAAAAMYFnQT1ABAAAAAAAAAAxgWQBPoAEQAAAAAAAADGBYkA
/gARAAAAAAADEIYYogMGABEAAAABAAkEAAABAGoAACABACkAACACALsCACAAAAAA
ACABACkAACAAAAAAAAABAKoEACAAAAAAACAAAAAAACABACkAACABAJwDACAAAAAA
ACABABsDACAAAAAAAAABAA4ECQCiAwEAEQCiAwYAGQCiAwoAKQCiAxAAMQCiAxAA
OQCiAxAAQQCiAxAASQCiAxAAUQCiAxAAWQCiAxAAYQCiAxUAaQCiAxAAcQCiAxAA
eQCiAxAAqQCiAxoAsQCpAEIAuQBlA0cAuQDrBEsAwQAzBFEAyQARAFYAuQAUBFsA
uQD3BEsA0QDfAmAA0QDQBEcA2QCzAGUA2QCvAmsAuQBtAnAAuQCWAnUA4QBZA3sA
iQC3BH8AiQBaAoUA8QDWAAYAiQBaAosAuQBoBJ8AkQBlAKYAkQBQAK8AkQAzAK8A
kQAjAK8AgQCiAwYALgALAAIBLgATAAsBLgAbACoBLgAjADMBLgArAD4BLgAzAD4B
LgA7AD4BLgBDADMBLgBLAEQBLgBTAD4BLgBbAD4BLgBjAFwBLgBrAIYBLgBzAJMB
gwBjAOEBgwB7AAsCowBjABQCBwDFAAkAxQALAMUADQDFAA8AxQATAMcAFQDJABcA
xQAZAMcAGwDHAB0AywAfAMsAIACUAASAAAABAAAAAAAAAAAAAAAAAEoAAAAEAAAA
AAAAAAAAAACzAG4AAAAAAAQAAAAAAAAAAAAAALwAoAIAAAAAAwACAAQAAgAFAAIA
AAAAAABNaWNyb3NvZnQuV2luMzIAVG9VSW50MzIAPE1vZHVsZT4AZ2V0X0IAbW9u
aXRvcklEAGdldF9HAFdpbkFQSQBTeXN0ZW0uSU8AV2FsbFAAZ2V0X1IAR2V0TW9u
aXRvclJFQ1QARnJvbUFyZ2IAbXNjb3JsaWIAUmVnaXN0cnlWYWx1ZUtpbmQARW5h
YmxlAElEaXNwb3NhYmxlAEZpbGUAQ29uc29sZQBXcml0ZUxpbmUAQ29tYmluZQBD
b21JbnRlcmZhY2VUeXBlAFZhbHVlVHlwZQBEaXNwb3NlAEd1aWRBdHRyaWJ1dGUA
RGVidWdnYWJsZUF0dHJpYnV0ZQBDb21WaXNpYmxlQXR0cmlidXRlAEFzc2VtYmx5
VGl0bGVBdHRyaWJ1dGUASW50ZXJmYWNlVHlwZUF0dHJpYnV0ZQBBc3NlbWJseVRy
YWRlbWFya0F0dHJpYnV0ZQBUYXJnZXRGcmFtZXdvcmtBdHRyaWJ1dGUAQXNzZW1i
bHlGaWxlVmVyc2lvbkF0dHJpYnV0ZQBBc3NlbWJseUNvbmZpZ3VyYXRpb25BdHRy
aWJ1dGUAQXNzZW1ibHlEZXNjcmlwdGlvbkF0dHJpYnV0ZQBDb21waWxhdGlvblJl
bGF4YXRpb25zQXR0cmlidXRlAEFzc2VtYmx5UHJvZHVjdEF0dHJpYnV0ZQBBc3Nl
bWJseUNvcHlyaWdodEF0dHJpYnV0ZQBBc3NlbWJseUNvbXBhbnlBdHRyaWJ1dGUA
UnVudGltZUNvbXBhdGliaWxpdHlBdHRyaWJ1dGUAU2V0VmFsdWUAV2FsbFAuZXhl
AEluZGV4T2YAU3lzdGVtLlJ1bnRpbWUuVmVyc2lvbmluZwBTdHJpbmcAU3Vic3Ry
aW5nAFN5c3RlbS5EcmF3aW5nAEdldEZ1bGxQYXRoAFdQcGF0aABTeXN0ZW0AQm90
dG9tAE1haW4AQXBwRG9tYWluAGdldF9DdXJyZW50RG9tYWluAFN5c3RlbS5SZWZs
ZWN0aW9uAEdldFBvc2l0aW9uAFNldFBvc2l0aW9uAHBvc2l0aW9uAFRvcABDaGFy
AElEZXNrdG9wV2FsbHBhcGVyAEdldFdhbGxwYXBlcgBTZXRXYWxscGFwZXIAQ3Vy
cmVudFVzZXIAVG9Mb3dlcgBHZXRCYWNrZ3JvdW5kQ29sb3IAU2V0QmFja2dyb3Vu
ZENvbG9yAEludENvbG9yAGNvbG9yAC5jdG9yAFN5c3RlbS5EaWFnbm9zdGljcwBT
eXN0ZW0uUnVudGltZS5JbnRlcm9wU2VydmljZXMAU3lzdGVtLlJ1bnRpbWUuQ29t
cGlsZXJTZXJ2aWNlcwBEZWJ1Z2dpbmdNb2RlcwBhcmdzAGl0ZW1zAENvbnRhaW5z
AERlc2t0b3BXYWxscGFwZXJDbGFzcwBFeGlzdHMAR2V0TW9uaXRvckRldmljZVBh
dGhBdABSZWN0AE9iamVjdABMZWZ0AFJpZ2h0AFNwbGl0AEdldE1vbml0b3JEZXZp
Y2VQYXRoQ291bnQAQ29udmVydABHZXRTbGlkZXNob3cAU2V0U2xpZGVzaG93AG1v
bml0b3JJbmRleABDcmVhdGVTdWJLZXkAUmVnaXN0cnlLZXkAZ2V0X0Jhc2VEaXJl
Y3RvcnkAUmVnaXN0cnkAb3BfRXF1YWxpdHkAb3BfSW5lcXVhbGl0eQAAAAAAAQBN
UwBlAHQAIAB3AGEAbABsAHAAYQBwAGUAcgAgAGYAbwByACAAbwBuAGUAIABvAHIA
IABtAG8AcgBlACAAbQBvAG4AaQB0AG8AcgBzAACAmVUAcwBhAGcAZQA6ACAAVwBh
AGwAbABQAC4AZQB4AGUAIABbAE0AbwBuAGkAdABvAHIASQBuAGQAZQB4AF0AIABb
AEkAbQBhAGcAZQBGAGkAbABlAFAAYQB0AGgAXQAgAFsAUABvAHMAaQB0AGkAbwBu
AF0AIABbAEIAYQBjAGsAZwByAG8AdQBuAGQAQwBvAGwAbwByAF0AAFFQAGEAcgBh
AG0AZQB0AGUAcgBzACAAYwBhAG4AIABiAGUAIABzAHAAZQBjAGkAZgBpAGUAZAAg
AGkAbgAgAGEAbgB5ACAAbwByAGQAZQByAABJTQBvAG4AaQB0AG8AcgBJAG4AZABl
AHgAIABpAHMAIABhACAAegBlAHIAbwAtAGIAYQBzAGUAZAAgAGkAbgB0AGUAZwBl
AHIAAWNJAG0AYQBnAGUARgBpAGwAZQBQAGEAdABoACAAYwBhAG4AIABiAGUAIABh
AG4AIABhAGIAcwBvAGwAdQB0AGUAIABvAHIAIAByAGUAbABhAHQAaQB2AGUAIABw
AGEAdABoAACAhUkAZgAgAE0AbwBuAGkAdABvAHIASQBuAGQAZQB4ACAAaQBzACAA
bwBtAGkAdAB0AGUAZAAsACAAdwBhAGwAbABwAGEAcABlAHIAIAB3AGkAbABsACAA
YgBlACAAcwBlAHQAIABmAG8AcgAgAGEAbABsACAAbQBvAG4AaQB0AG8AcgBzAACA
k0kAZgAgAEkAbQBhAGcAZQBGAGkAbABlAFAAYQB0AGgAIABpAHMAIABvAG0AaQB0
AHQAZQBkACwAIABNAG8AbgBpAHQAbwByAEkAbgBkAGUAeAAgAHcAYQBsAGwAcABh
AHAAZQByACAAcABhAHQAaAAgAHcAaQBsAGwAIABiAGUAIAByAGUAdAB1AHIAbgBl
AGQAAHNQAG8AcwBpAHQAaQBvAG4AIABjAGEAbgAgAGIAZQAgAG8AbgBlACAAbwBm
ADoAIABDAGUAbgB0AGUAcgAgAFQAaQBsAGUAIABTAHQAcgBlAHQAYwBoACAARgBp
AHQAIABGAGkAbABsACAAUwBwAGEAbgAAgJNJAGYAIABQAG8AcwBpAHQAaQBvAG4A
IABpAHMAIABvAG0AaQB0AHQAZQBkACwAIABwAG8AcwBpAHQAaQBvAG4AIABpAHMA
IAB1AG4AYwBoAGEAbgBnAGUAZAAgAGYAbwByACAAQwBlAG4AdABlAHIAIABTAHQA
cgBlAHQAYwBoACAARgBpAHQAIABGAGkAbABsAABpSQBmACAAUABvAHMAaQB0AGkA
bwBuACAAaQBzACAAbwBtAGkAdAB0AGUAZAAsACAAUwBwAGEAbgAgAGEAbgBkACAA
VABpAGwAZQAgAHIAZQB2AGUAcgB0ACAAdABvACAARgBpAGwAbAAAgI1CAGEAYwBr
AGcAcgBvAHUAbgBkAEMAbwBsAG8AcgAgAGkAcwAgAHMAcABlAGMAaQBmAGkAZQBk
ACAAYQBzACAAcgAsAGcALABiAC4AIABFAHgAYQBtAHAAbABlACAAKABDAG8AbwBs
ACAAYgBsAHUAZQApADoAIAA0ADUALAAxADIANQAsADEANQA0AAANYwBlAG4AdABl
AHIAAAl0AGkAbABlAAAPcwB0AHIAZQB0AGMAaAAAB2YAaQB0AAAJZgBpAGwAbAAA
CXMAcABhAG4AAAMsAAAHVQBJAEQAAAdBAGwAbAAAEVMAbwBmAHQAdwBhAHIAZQAA
C1cAYQBsAGwAUAAAGU0AbwBuAGkAdABvAHIAQwBvAHUAbgB0AAAPTQBvAG4AaQB0
AG8AcgAADVcAUABwAGEAdABoAABpTQBpAGMAcgBvAHMAbwBmAHQAXABXAGkAbgBk
AG8AdwBzAFwAQwB1AHIAcgBlAG4AdABWAGUAcgBzAGkAbwBuAFwARQB4AHAAbABv
AHIAZQByAFwAVwBhAGwAbABwAGEAcABlAHIAcwAAHUIAYQBjAGsAZwByAG8AdQBu
AGQAVAB5AHAAZQAAIGK1gArDJk2F5aJd/Y2U4AAEIAEBCAMgAAEFIAEBEREEIAEB
DgQgAQECBSABARFRIQcaDgkOCA4OAhIQCAICAgICAgICAgICAhJFEkUSRRJFAgQA
AQEOAyAADgUAAgIODgQAAQIOBAABCQ4EIAECDgQAABJpBQACDg4OBAABDg4EIAEI
DgUgAg4ICAMGEkUFIAESRQ4FIAIBDhwIIAMBDhwRgIEKBwYJCQkdDhFJCQYgAR0O
HQMIAAQRSQgICAgDIAAFCLd6XFYZNOCJCLA/X38R1Qo6ARUBCAEbAQcCBggFAAEB
HQ4FIAIBDg4EIAEODgQgAQ4JAyAACQUgAREMDgQgAQEJBCABARgDIAAYAyAAAggB
AAgAAAAAAB4BAAEAVAIWV3JhcE5vbkV4Y2VwdGlvblRocm93cwEIAQAHAQAAAAAK
AQAFV2FsbFAAAAUBAAAAABcBABJDb3B5cmlnaHQgwqkgIDIwMjIAACkBACQwNTI0
ZDllOS02MWFlLTQ1ZTEtYTk4Ny00MjllOWZmMGExMjQAAAwBAAcxLjAuMC4wAABN
AQAcLk5FVEZyYW1ld29yayxWZXJzaW9uPXY0LjYuMQEAVA4URnJhbWV3b3JrRGlz
cGxheU5hbWUULk5FVCBGcmFtZXdvcmsgNC42LjEpAQAkQjkyQjU2QTktOEI1NS00
RTE0LTlBODktMDE5OUJCQjZGOTNCAAAIAQABAAAAAAApAQAkQzJDRjMxMTAtNDYw
RS00ZmMxLUI5RDAtOEExQzBDOUNDNEJEAAAAAAAAAABW4HqvAAAAAAIAAABYAAAA
pDcAAKQZAAAAAAAAAAAAAAAAAAAQAAAAAAAAAAAAAAAAAAAAUlNEUzRHK+zt7vRL
lYuG2g6JLK0BAAAAQzpcVXNlcnNcTGVzIEZlcmNoXHNvdXJjZVxyZXBvc1xXYWxs
UFxXYWxsUFxvYmpcRGVidWdcV2FsbFAucGRiACQ4AAAAAAAAAAAAAD44AAAAIAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAwOAAAAAAAAAAAAAAAAF9Db3JFeGVNYWluAG1z
Y29yZWUuZGxsAAAAAAD/JQAgQAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAgAQAAAAIAAAgBgAAABQAACA
AAAAAAAAAAAAAAAAAAABAAEAAAA4AACAAAAAAAAAAAAAAAAAAAABAAAAAACAAAAA
AAAAAAAAAAAAAAAAAAABAAEAAABoAACAAAAAAAAAAAAAAAAAAAABAAAAAACMAwAA
kEAAAPwCAAAAAAAAAAAAAPwCNAAAAFYAUwBfAFYARQBSAFMASQBPAE4AXwBJAE4A
RgBPAAAAAAC9BO/+AAABAAAAAQAAAAAAAAABAAAAAAA/AAAAAAAAAAQAAAABAAAA
AAAAAAAAAAAAAAAARAAAAAEAVgBhAHIARgBpAGwAZQBJAG4AZgBvAAAAAAAkAAQA
AABUAHIAYQBuAHMAbABhAHQAaQBvAG4AAAAAAAAAsARcAgAAAQBTAHQAcgBpAG4A
ZwBGAGkAbABlAEkAbgBmAG8AAAA4AgAAAQAwADAAMAAwADAANABiADAAAAAaAAEA
AQBDAG8AbQBtAGUAbgB0AHMAAAAAAAAAIgABAAEAQwBvAG0AcABhAG4AeQBOAGEA
bQBlAAAAAAAAAAAANAAGAAEARgBpAGwAZQBEAGUAcwBjAHIAaQBwAHQAaQBvAG4A
AAAAAFcAYQBsAGwAUAAAADAACAABAEYAaQBsAGUAVgBlAHIAcwBpAG8AbgAAAAAA
MQAuADAALgAwAC4AMAAAADQACgABAEkAbgB0AGUAcgBuAGEAbABOAGEAbQBlAAAA
VwBhAGwAbABQAC4AZQB4AGUAAABIABIAAQBMAGUAZwBhAGwAQwBvAHAAeQByAGkA
ZwBoAHQAAABDAG8AcAB5AHIAaQBnAGgAdAAgAKkAIAAgADIAMAAyADIAAAAqAAEA
AQBMAGUAZwBhAGwAVAByAGEAZABlAG0AYQByAGsAcwAAAAAAAAAAADwACgABAE8A
cgBpAGcAaQBuAGEAbABGAGkAbABlAG4AYQBtAGUAAABXAGEAbABsAFAALgBlAHgA
ZQAAACwABgABAFAAcgBvAGQAdQBjAHQATgBhAG0AZQAAAAAAVwBhAGwAbABQAAAA
NAAIAAEAUAByAG8AZAB1AGMAdABWAGUAcgBzAGkAbwBuAAAAMQAuADAALgAwAC4A
MAAAADgACAABAEEAcwBzAGUAbQBiAGwAeQAgAFYAZQByAHMAaQBvAG4AAAAxAC4A
MAAuADAALgAwAAAAnEMAAOoBAAAAAAAAAAAAAO+7vzw/eG1sIHZlcnNpb249IjEu
MCIgZW5jb2Rpbmc9IlVURi04IiBzdGFuZGFsb25lPSJ5ZXMiPz4NCg0KPGFzc2Vt
Ymx5IHhtbG5zPSJ1cm46c2NoZW1hcy1taWNyb3NvZnQtY29tOmFzbS52MSIgbWFu
aWZlc3RWZXJzaW9uPSIxLjAiPg0KICA8YXNzZW1ibHlJZGVudGl0eSB2ZXJzaW9u
PSIxLjAuMC4wIiBuYW1lPSJNeUFwcGxpY2F0aW9uLmFwcCIvPg0KICA8dHJ1c3RJ
bmZvIHhtbG5zPSJ1cm46c2NoZW1hcy1taWNyb3NvZnQtY29tOmFzbS52MiI+DQog
ICAgPHNlY3VyaXR5Pg0KICAgICAgPHJlcXVlc3RlZFByaXZpbGVnZXMgeG1sbnM9
InVybjpzY2hlbWFzLW1pY3Jvc29mdC1jb206YXNtLnYzIj4NCiAgICAgICAgPHJl
cXVlc3RlZEV4ZWN1dGlvbkxldmVsIGxldmVsPSJhc0ludm9rZXIiIHVpQWNjZXNz
PSJmYWxzZSIvPg0KICAgICAgPC9yZXF1ZXN0ZWRQcml2aWxlZ2VzPg0KICAgIDwv
c2VjdXJpdHk+DQogIDwvdHJ1c3RJbmZvPg0KPC9hc3NlbWJseT4AAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAwAAAMAAAAUDgAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
-----END CERTIFICATE-----
-->
